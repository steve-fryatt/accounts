REM >!Accounts
REM
REM Copyright 1992-2014, Stephen Fryatt (info@stevefryatt.org.uk)
REM
REM This file is part of Accounts:
REM
REM   http://www.stevefryatt.org.uk/software/
REM
REM Licensed under the EUPL, Version 1.1 only (the "Licence");
REM You may not use this work except in compliance with the
REM Licence.
REM
REM You may obtain a copy of the Licence at:
REM
REM   http://joinup.ec.europa.eu/software/page/eupl
REM
REM Unless required by applicable law or agreed to in
REM writing, software distributed under the Licence is
REM distributed on an "AS IS" basis, WITHOUT WARRANTIES
REM OR CONDITIONS OF ANY KIND, either express or implied.
REM
REM See the Licence for the specific language governing
REM permissions and limitations under the Licence.

LIBRARY "BASIC:Icon"
LIBRARY "BASIC:Menu"
LIBRARY "BASIC:Message"
LIBRARY "BASIC:Resources"
LIBRARY "BASIC:String"
LIBRARY "BASIC:Template"
LIBRARY "BASIC:Url"
LIBRARY "BASIC:WimpError"
LIBRARY "BASIC:WimpSprite"
LIBRARY "BASIC:Window"

build_version$="5.19"
build_date$="26th August 1994"

PROCwimperror_initialise("Accounts", "!accounts")
ON ERROR result% = FNwimperror_program : END

SYS "XOS_GetEnv" TO CommandLine$

PROCinitialise

ON ERROR IF NOT FNcheck_error(ERR) THEN Quit% = FNwimperror_program

WHILE NOT Quit%
	PROCpoll
ENDWHILE

PROCloose_fonts

IF RestartShutdown% THEN SYS "Wimp_ProcessKey", &1FC

SYS "Wimp_CloseDown"
END


REM Perform a Wimp_Poll
:
DEF PROCpoll
LOCAL err%

IF IntroUpAt% <> 0 THEN
	SYS "Wimp_PollIdle", 0, b%, IntroDisplayTime% + IntroUpAt% TO reason%
ELSE
	SYS "Wimp_Poll", 1, b% TO reason%
ENDIF

CASE reason% OF
WHEN 0
	PROCclose_intro
WHEN 1
	PROCredraw
WHEN 2
	SYS "Wimp_OpenWindow",,b%
	IF !b% = MainWindow% OR !b% = prefs% THEN
		IF !b% = MainWindow% THEN
			!q% = TBarWindow%
			SYS "Wimp_GetWindowState",,q%
			q%!4 = b%!4
			q%!16 = b%!16
			IF FNmenu_state(MainMenu%, 8) THEN q%!8 = b%!16 - 136 ELSE q%!8 = b%!16 - 36
			q%!12 = b%!12
			q%!20 = b%!20
			IF FNmenu_state(MainMenu%, 8) q%!24 = 0 ELSE q%!24 = -96
			q%!28 = b%!28
			SYS "Wimp_OpenWindow",,q%
			IF FNmenu_state(MainMenu%, 7) AND join_main% THEN
				!q% = enter%
				SYS "Wimp_GetWindowState",,q%
				q%!4 = b%!4 + 40
				IF b%!8 > 236 THEN over% = 0
				IF b%!8 <= 236 AND b%!8 > 0 THEN over% = 236 - b%!8
				IF b%!8 <= 0 THEN over% = 236
				q%!16 = b%!8 -44 + over%
				q%!8 = b%!8 - 236 + over%
				q%!12 = b%!4 + 1236
				q%!20 = 0
				q%!24 = 0
				q%!28 = TBarWindow%
				SYS "Wimp_OpenWindow",,q%
				b%!28 = enter%
			ELSE
				b%!28 = TBarWindow%
			ENDIF
			SYS "Wimp_OpenWindow",,b%
		ELSE
			!q% = prefspane%
			SYS "Wimp_GetWindowState",,q%
			q%!4 = b%!4 + 22
			q%!16 = b%!8 + 402
			q%!8 = b%!8 + 24
			q%!12 = b%!4 + 502
			q%!28 = b%!28
			SYS "Wimp_OpenWindow",,q%
			b%!28 = prefspane%
			SYS "Wimp_OpenWindow",,b%
		ENDIF
	ELSE
		SYS "Wimp_OpenWindow",,b%
	ENDIF
WHEN 3
	SYS "Wimp_CloseWindow",,b%
	IF !b% = MainWindow% THEN
		PROCwindow_close(enter%)
		PROCwindow_close(TBoxWindow%)
		PROCwindow_close(TBarWindow%)
	ENDIF
	IF !b% = prefs% THEN PROCwindow_close(prefspane%)
WHEN 6
	PROCclick(b%!8, b%!12, b%!16)
WHEN 7
	PROCstart_save(savesize%, savetype%, savepoint%)
WHEN 8
	PROCkey_press(b%!24, !b%, b%!4)
WHEN 9
	PROCmenu_selection(b%)
WHEN 17, 18
	PROCuser_message(b%)
WHEN 19
	IF b%!16 = &4AF80 THEN PROCurl_bounce(b%)
ENDCASE
ENDPROC


REM Close the splash screen if it has been on screen long enough.
:
DEF PROCclose_intro
IF (IntroUpAt% = 0) OR (TIME < IntroUpAt% + IntroDisplayTime%) THEN ENDPROC

PROCwindow_close(intro%)
IntroUpAt%=0
ENDPROC


REM Process mouse click events
REM
REM \param button%		The clicked mouse button.
REM \param window%		The window under the mouse.
REM \param icon%		The icon under the mouse.
:
DEF PROCclick(button%, window%, icon%)
LOCAL size%, loop%

CASE window% OF
WHEN -2
	CASE button% OF
	WHEN 2
		PROCmenu_create_iconbar(IconbarMenu%, !b%)
	WHEN 4
		PROCopen_window(MainWindow%)
		PROCset_extent(TRUE)
		IF FNmenu_state(MainMenu%, 7) THEN
			PROCopen_window(enter%)
			PROCicon_put_caret_at_end(enter%, 6)
		ENDIF
		IF FNmenu_state(MainMenu%, 6) THEN PROCopen_window(TBoxWindow%)
	ENDCASE

WHEN InfoWindow%
	IF button% = 4 AND icon% = 8 THEN
		PROCurl_launch(FNmessage_lookup("SupportURL"))
		PROCmenu_create(-1,0,0)
	ENDIF

WHEN TBoxWindow%
CASE button% OF
WHEN 1,4
CASE icon% OF
WHEN 0,1,2,3 :PROCaddval(icon%)
WHEN 4       :PROCdelete
ENDCASE
WHEN 2 :PROCupdate_menu
PROCmenu_create(MainMenu%,!b%,b%!4)
ENDCASE
WHEN TBarWindow%
CASE button% OF
WHEN 1,4
CASE icon% OF
WHEN 1,2,3,4 :PROCaddval(icon%-1)
WHEN 5       :PROCdelete
WHEN 6       :PROCcut
WHEN 7       :PROCcopy
WHEN 8       :PROCclear_select
WHEN 9       :PROCwindow_open_as_menu_at(csv%,!b%,b%!4)
WHEN 10      :PROCwindow_open_as_menu_at(find%,!b%,b%!4)
WHEN 11      :PROCwindow_open_as_menu_at(GotoWindow%,!b%,b%!4)
WHEN 12      :PROCgoto_selection
WHEN 13      :PROCwindow_open_as_menu_at(HeadWindow%,!b%,b%!4)
WHEN 14      :PROCwindow_open_as_menu_at(printset%,!b%,b%!4)
ENDCASE
WHEN 256,1024
CASE icon% OF
WHEN 9       :PROCwindow_open_as_menu_at(save%,!b%,b%!4)
WHEN 14      :PROCwindow_open_as_menu_at(print%,!b%,b%!4)
ENDCASE
WHEN 2 :PROCupdate_menu
PROCmenu_create(MainMenu%,!b%,b%!4)
ENDCASE
WHEN MainWindow%
CASE button% OF
WHEN 4 :IF icon%=-1 PROCselect(b%!4)
!q%=enter%
SYS "Wimp_GetWindowState",,q%
IF (q%!32 AND (1<<16)) THENPROCicon_put_caret_at_end(enter%,6)
WHEN 2 :PROCupdate_menu
PROCmenu_create(MainMenu%,!b%,b%!4)
ENDCASE
WHEN enter%
CASE button% OF
WHEN 1,4
CASE icon% OF
WHEN 0,1 :PROCcheck_select(window%,icon%)
ENDCASE
WHEN 2 :PROCupdate_menu
PROCmenu_create(MainMenu%,!b%,b%!4)
ENDCASE
WHEN save%
savetype%=account_file%
savesize%=((entries%*15)+(entries%*22))
savepoint%=file%
CASE icon% OF
WHEN 0 :IF (button% AND 64) PROCdrag_box(save%)
WHEN 2 :IF (button% AND 5 ) PROCquick_save(file%)
ENDCASE
WHEN text%
savetype%=text_file%
size%=VAL($text2%)-VAL($text1%)
savesize%=size%*80
savepoint%=tfile%
CASE icon% OF
WHEN 0 :IF (button% AND 64) PROCdrag_box(text%)
WHEN 2 :IF (button% AND 5 ) PROCquick_save(tfile%)
WHEN 6
IF button%=4 THEN
IF VAL($text1%)<(entries%-1) $text1%=STR$(VAL($text1%)+1)
PROCredraw_part(text%,148,-216,88,48)
ELSE
IF VAL($text1%)>1            $text1%=STR$(VAL($text1%)-1)
PROCredraw_part(text%,148,-216,88,48)
ENDIF
WHEN 4
IF button%=4 THEN
IF VAL($text1%)>1            $text1%=STR$(VAL($text1%)-1)
PROCredraw_part(text%,148,-216,88,48)
ELSE
IF VAL($text1%)<(entries%-1) $text1%=STR$(VAL($text1%)+1)
PROCredraw_part(text%,148,-216,88,48)
ENDIF
WHEN 7
IF button%=4 THEN
IF VAL($text2%)<(entries%-1) $text2%=STR$(VAL($text2%)+1)
PROCredraw_part(text%,148,-288,88,48)
ELSE
IF VAL($text2%)>1            $text2%=STR$(VAL($text2%)-1)
PROCredraw_part(text%,148,-288,88,48)
ENDIF
WHEN 5
IF button%=4 THEN
IF VAL($text2%)>1            $text2%=STR$(VAL($text2%)-1)
PROCredraw_part(text%,148,-288,88,48)
ELSE
IF VAL($text2%)<(entries%-1) $text2%=STR$(VAL($text2%)+1)
PROCredraw_part(text%,148,-288,88,48)
ENDIF
ENDCASE
WHEN csv%
savetype%=csv_file%
size%=VAL($csv2%)-VAL($csv1%)
savesize%=(size%*SUMLEN(text$()))+(size%*18)
savepoint%=cfile%
CASE icon% OF
WHEN 0 :IF (button% AND 64) PROCdrag_box(csv%)
WHEN 2 :IF (button% AND 5 ) PROCquick_save(cfile%)
WHEN 6
IF button%=4 THEN
IF VAL($csv1%)<(entries%-1) $csv1%=STR$(VAL($csv1%)+1)
PROCredraw_part(csv%,148,-216,88,48)
ELSE
IF VAL($csv1%)>1            $csv1%=STR$(VAL($csv1%)-1)
PROCredraw_part(csv%,148,-216,88,48)
ENDIF
WHEN 4
IF button%=4 THEN
IF VAL($csv1%)>1            $csv1%=STR$(VAL($csv1%)-1)
PROCredraw_part(csv%,148,-216,88,48)
ELSE
IF VAL($csv1%)<(entries%-1) $csv1%=STR$(VAL($csv1%)+1)
PROCredraw_part(csv%,148,-216,88,48)
ENDIF
WHEN 7
IF button%=4 THEN
IF VAL($csv2%)<(entries%-1) $csv2%=STR$(VAL($csv2%)+1)
PROCredraw_part(csv%,148,-288,88,48)
ELSE
IF VAL($csv2%)>1            $csv2%=STR$(VAL($csv2%)-1)
PROCredraw_part(csv%,148,-288,88,48)
ENDIF
WHEN 5
IF button%=4 THEN
IF VAL($csv2%)>1            $csv2%=STR$(VAL($csv2%)-1)
PROCredraw_part(csv%,148,-288,88,48)
ELSE
IF VAL($csv2%)<(entries%-1) $csv2%=STR$(VAL($csv2%)+1)
PROCredraw_part(csv%,148,-288,88,48)
ENDIF
ENDCASE
WHEN print%
CASE icon% OF
WHEN 3
IF button%=4 THEN
IF VAL($print1%)<(entries%-1) $print1%=STR$(VAL($print1%)+1)
PROCredraw_part(print%,150,-108,88,48)
ELSE
IF VAL($print1%)>1            $print1%=STR$(VAL($print1%)-1)
PROCredraw_part(print%,150,-108,88,48)
ENDIF
WHEN 1
IF button%=4 THEN
IF VAL($print1%)>1            $print1%=STR$(VAL($print1%)-1)
PROCredraw_part(print%,150,-108,88,48)
ELSE
IF VAL($print1%)<(entries%-1) $print1%=STR$(VAL($print1%)+1)
PROCredraw_part(print%,150,-108,88,48)
ENDIF
WHEN 4
IF button%=4 THEN
IF VAL($print2%)<(entries%-1) $print2%=STR$(VAL($print2%)+1)
PROCredraw_part(print%,150,-180,88,48)
ELSE
IF VAL($print2%)>1            $print2%=STR$(VAL($print2%)-1)
PROCredraw_part(print%,150,-180,88,48)
ENDIF
WHEN 2
IF button%=4 THEN
IF VAL($print2%)>1            $print2%=STR$(VAL($print2%)-1)
PROCredraw_part(print%,150,-180,88,48)
ELSE
IF VAL($print2%)<(entries%-1) $print2%=STR$(VAL($print2%)+1)
PROCredraw_part(print%,150,-180,88,48)
ENDIF
WHEN 11
PROCprint
PROCmenu_create(-1,0,0)
WHEN 12
PROCmenu_create(-1,0,0)
ENDCASE
WHEN GotoWindow%
IF icon%=1 PROCgoto
WHEN SLineWindow%
IF icon%=1 PROCkey_select
WHEN find%
CASE icon% OF
WHEN 3,4 :PROCcheck_select(window%,icon%)
WHEN 5 :PROCfind(TRUE,FALSE,FALSE)
WHEN 6 :PROCmenu_create(-1,0,0)
WHEN 7 :PROCfind(TRUE,TRUE,FALSE)
ENDCASE
WHEN foundw%
CASE icon% OF
WHEN 4 :IF button%=4 OR button%=1 PROCfind(FALSE,FALSE,FALSE)
WHEN 6 :IF button%=4 OR button%=1 PROCfind(FALSE,FALSE,TRUE)
WHEN 7 :IF button%=4 OR button%=1 PROCfind(FALSE,TRUE,FALSE)
WHEN 5
PROCwindow_close(foundw%)
PROCclear_button(foundw%,4)
PROCclear_button(foundw%,6)
PROCclear_button(foundw%,7)
$foundtext%=""
ENDCASE
WHEN HeadWindow%
CASE icon% OF
WHEN 2 :PROCnew_header
WHEN 3 :PROCmenu_create(-1,0,0)
ENDCASE
WHEN printset%
CASE icon% OF
WHEN 18 :PROCcancel_printcodes
WHEN 19 :PROCsave_printcodes
WHEN 20 :PROCset_printcodes
WHEN 21
IF button%=4 THEN
IF VAL($page_length%)<999 $page_length%=STR$(VAL($page_length%)+1)
PROCredraw_part(printset%,220,-68,80,48)
ELSE
IF VAL($page_length%)>1   $page_length%=STR$(VAL($page_length%)-1)
PROCredraw_part(printset%,220,-68,80,48)
WHEN 22
IF button%=4 THEN
IF VAL($page_length%)>1   $page_length%=STR$(VAL($page_length%)-1)
PROCredraw_part(printset%,220,-68,80,48)
ELSE
IF VAL($page_length%)<999 $page_length%=STR$(VAL($page_length%)+1)
PROCredraw_part(printset%,220,-68,80,48)
ENDCASE
WHEN prefs%
CASE icon% OF
WHEN 0 :PROCload_dft_prefs(FALSE)
WHEN 1 :PROCsave_prefs(TRUE)
WHEN 2 :PROCload_dft_prefs(TRUE)
WHEN 3 :PROCload_prefs(FALSE)
PROCwindow_close(prefs%)
PROCwindow_close(prefspane%)
WHEN 4 :PROCsave_prefs(FALSE)
ENDCASE
WHEN prefspane%
CASE icon% OF
WHEN 12,13,14 :PROCcheck_select(window%,icon%)
WHEN 16,17,18,19 :PROCcheck_select(window%,icon%)
WHEN 47,48,49 :PROCcheck_select(window%,icon%)
WHEN 34,35,36,37,52
FOR loop%=0 TO 15
PROCmenu_set_state(ColourMenu%,loop%,0,0)
NEXT loop%
CASE icon% OF
WHEN 34 :PROCmenu_set_state(ColourMenu%,VAL($entry_c%),1,0)
WHEN 35 :PROCmenu_set_state(ColourMenu%,VAL($numb_c%),1,0)
WHEN 36 :PROCmenu_set_state(ColourMenu%,VAL($sel_c%),1,0)
WHEN 37 :PROCmenu_set_state(ColourMenu%,VAL($unused_c%),1,0)
WHEN 52 :PROCmenu_set_state(ColourMenu%,VAL($overdrawn_c%),1,0)
ENDCASE
PROCmenu_create(ColourMenu%,!b%,b%!4)
prefs_menu%=icon%
ENDCASE
WHEN merge%
CASE icon% OF
WHEN 0 :PROCmerge_files(0)
WHEN 1 :PROCmenu_create(-1,0,0)
WHEN 2 :IF button%<>2 PROCmerge_files(1)
WHEN 3 :IF button%<>2 PROCmerge_files(2)
ENDCASE
WHEN loadcsv%
CASE icon% OF
WHEN 3  :IF button%=4 PROCcsv_columns(0,-1) ELSEPROCcsv_columns(0,1)
WHEN 4  :IF button%=4 PROCcsv_columns(0,1) ELSEPROCcsv_columns(0,-1)
WHEN 5  :IF button%=4 PROCcsv_columns(1,-1) ELSEPROCcsv_columns(1,1)
WHEN 6  :IF button%=4 PROCcsv_columns(1,1) ELSEPROCcsv_columns(1,-1)
WHEN 7  :IF button%=4 PROCcsv_columns(2,-1) ELSEPROCcsv_columns(2,1)
WHEN 8  :IF button%=4 PROCcsv_columns(2,1) ELSEPROCcsv_columns(2,-1)
WHEN 12 :PROCmenu_create(-1,0,0)
WHEN 13 :PROCcsv_load
ENDCASE
ENDCASE
ENDPROC



REM Process selections from menus.
REM
REM \param block%		The menu selection block.
:
DEF PROCmenu_selection(block%)
LOCAL reopen%, loop%

SYS "Wimp_GetPointerInfo",,q%
reopen% = (q%!8 = 1)

CASE FNmenu_current_handle OF
WHEN IconbarMenu%
	CASE b%!0 OF
	WHEN 1
		OSCLI("%Filer_Run <Account$Dir>.!Help")
	WHEN 2
		PROCopen_window(prefs%)
		PROCicon_put_caret_at_end(prefspane%, 23)
	WHEN 3
		Quit% = FNsaved
	ENDCASE

WHEN ColourMenu%
	FOR loop% = 0 TO 15
		PROCmenu_set_state(ColourMenu%, loop%, 0, 0)
	NEXT loop%

	CASE prefs_menu% OF
	WHEN 34
		$entry_c% = STR$(b%!0)
		PROCredraw_part(prefspane%, 42, -1352, 50, 48)
		PROCmenu_set_state(ColourMenu%, VAL($entry_c%), 1, 0)
	WHEN 35
		$numb_c%=STR$(b%!0)
		PROCredraw_part(prefspane%,42,-1428,50,48)
		PROCmenu_set_state(ColourMenu%,VAL($numb_c%),1,0)
	WHEN 36
		$sel_c%=STR$(b%!0)
		PROCredraw_part(prefspane%,42,-1508,50,48)
		PROCmenu_set_state(ColourMenu%,VAL($sel_c%),1,0)
	WHEN 37
		$unused_c%=STR$(b%!0)
		PROCredraw_part(prefspane%,42,-1588,50,48)
		PROCmenu_set_state(ColourMenu%,VAL($unused_c%),1,0)
	WHEN 52
		$overdrawn_c%=STR$(b%!0)
		PROCredraw_part(prefspane%,42,-1668,50,48)
		PROCmenu_set_state(ColourMenu%,VAL($overdrawn_c%),1,0)
	ENDCASE

WHEN MainMenu%
	CASE b%!0 OF
	WHEN 0
		CASE b%!4 OF
			WHEN 0 :PROCwindow_open_as_menu_at(InfoWindow%,!b%,b%!4) : reopen% = FALSE
			WHEN 1 :PROCwindow_open_as_menu_at(fileinf%,!b%,b%!4) : reopen% = FALSE
			WHEN 2 :PROCwindow_open_as_menu_at(find%,!b%,b%!4) : reopen% = FALSE
			WHEN 3 :PROCwindow_open_as_menu_at(GotoWindow%,!b%,b%!4) : reopen% = FALSE
			WHEN 4 :PROCwindow_open_as_menu_at(HeadWindow%,!b%,b%!4) : reopen% = FALSE
			WHEN 5 :PROCclear
		ENDCASE
	WHEN 1
		CASE b%!4 OF
			WHEN -1,0 :PROCquick_save(file%)
			WHEN 1 :PROCwindow_open_as_menu_at(text%,!b%,b%!4) : reopen% = FALSE
			WHEN 2 :PROCwindow_open_as_menu_at(csv%,!b%,b%!4) : reopen% = FALSE
		ENDCASE
	WHEN 2
		IF b%!4 > -1 THEN PROCaddval(b%!4)
	WHEN 3
		CASE b%!4 OF
			WHEN -1,0 : PROCwindow_open_as_menu_at(SLineWindow%,!b%,b%!4) : reopen% = FALSE
			WHEN 1 :PROCremove
			WHEN 2 :PROCdelete
			WHEN 3 :PROCaddval(3)
			WHEN 4 :PROCclear_select
		ENDCASE
	WHEN 4
		CASE b%!4 OF
			WHEN 0 :PROCcut
			WHEN 1 :PROCcopy
			WHEN 2 :PROCcurrent
			WHEN 3 :PROCclip_clear
		ENDCASE
	WHEN 5
		CASE b%!4 OF
			WHEN -1,0 :PROCwindow_open_as_menu_at(print%,!b%,b%!4) : reopen% = FALSE
			WHEN 1 :PROCwindow_open_as_menu_at(printset%,!b%,b%!4) : reopen% = FALSE
		ENDCASE
	WHEN 6
		!b%=TBoxWindow%
		SYS "Wimp_GetWindowState",,b%
		IF b%!32 AND 1<<16 THEN
			PROCwindow_close(TBoxWindow%)
			PROCmenu_set_state(MainMenu%,6,0,0)
		ELSE
			PROCopen_window(TBoxWindow%)
			PROCmenu_set_state(MainMenu%,6,1,0)
		ENDIF
	WHEN 7
		!b%=enter%
		SYS "Wimp_GetWindowState",,b%
		IF b%!32 AND 1<<16 THEN
			PROCwindow_close(enter%)
			PROCmenu_set_state(MainMenu%,7,0,0)
		ELSE
			!b%=enter%
			SYS "Wimp_GetWindowState",,b%
			!q%=MainWindow%
			SYS "Wimp_GetWindowState",,q%
			b%!4=q%!4+40
			b%!16=q%!8-44
			b%!8=q%!8-236
			b%!12=q%!4+1236
			b%!20=0
			b%!24=0
			b%!28=TBarWindow%
			SYS "Wimp_OpenWindow",,b%
			PROCicon_put_caret_at_end(enter%,6)
			PROCmenu_set_state(MainMenu%,7,1,0)
		ENDIF
	WHEN 8
		IF FNmenu_state(MainMenu%,8) THEN
			!b%=TBarWindow%
			SYS "Wimp_GetWindowState",,b%
			!q%=MainWindow%
			SYS "Wimp_GetWindowState",,q%
			b%!4=q%!4
			b%!16=q%!16
			b%!8=q%!16-36
			b%!12=q%!12
			b%!20=0
			b%!24=0
			b%!28=q%!28
			SYS "Wimp_OpenWindow",,b%
			PROCmenu_set_state(MainMenu%,8,0,0)
		ELSE
			!b%=TBarWindow%
			SYS "Wimp_GetWindowState",,b%
			b%!4=q%!4
			b%!16=q%!16-100
			b%!8=q%!16-136
			b%!12=q%!12
			b%!20=0
			b%!24=0
			b%!28=q%!28
			SYS "Wimp_OpenWindow",,b%
			PROCmenu_set_state(MainMenu%,8,1,0)
		ENDIF

		PROCredraw_main
		PROCset_extent(0)
	ENDCASE
ENDCASE

IF reopen% THEN
	PROCupdate_menu
	PROCmenu_create(FNmenu_current_handle, 0, 0)
ENDIF
ENDPROC
:
DEF PROCkey_press(key%,whan%,icon%)
SYS "Wimp_GetPointerInfo",,b%
CASE key% OF
WHEN 4  :PROCdate(whan%,icon%,b%!20)
WHEN 13 :
CASE whan% OF
WHEN save%      :PROCquick_save(file%)
WHEN text%      :PROCquick_save(tfile%)
WHEN csv%       :PROCquick_save(cfile%)
WHEN find%      :PROCfind(TRUE,FALSE,FALSE)
WHEN HeadWindow%   :PROCnew_header
WHEN GotoWindow%      :PROCgoto
WHEN SLineWindow%   :PROCkey_select
WHEN print%     :PROCprint
WHEN printset%  :PROCset_printcodes
WHEN prefspane% :PROCsave_prefs(FALSE)
WHEN enter%
CASE icon% OF
WHEN 6  :PROCicon_put_caret_at_end(enter%,7)
WHEN 7  :PROCicon_put_caret_at_end(enter%,8)
WHEN 8
PROCicon_set_state(enter%,0,1,0,0)
PROCicon_set_state(enter%,1,0,0,0)
PROCaddval(0)
IF return_caret% THENPROCicon_put_caret_at_end(enter%,6)
WHEN 11 :PROCicon_put_caret_at_end(enter%,12)
WHEN 12 :PROCicon_put_caret_at_end(enter%,13)
WHEN 13
PROCicon_set_state(enter%,0,0,0,0)
PROCicon_set_state(enter%,1,1,0,0)
PROCaddval(0)
IF return_caret% THENPROCicon_put_caret_at_end(enter%,6)
ENDCASE
ENDCASE
WHEN 19 :PROCswap(whan%,icon%,b%!20)
WHEN 20 :PROCnew_date(whan%,icon%)
WHEN 26 :PROCclear_select
WHEN 30
CASE home_key% OF
WHEN 1 :PROCpage_home
WHEN 2 :PROCicon_put_caret_at_end(enter%,6)
WHEN 3 :PROCpage_home :PROCicon_put_caret_at_end(enter%,6)
ENDCASE
WHEN &180 :PROCwindow_open_as_menu_at(print%,!b%,b%!4)
WHEN &190 :PROCwindow_open_as_menu_at(printset%,!b%,b%!4)
WHEN &181 :PROCaddval(0)
WHEN &191
!b%=TBoxWindow%
SYS "Wimp_GetWindowState",,b%
IF b%!32 AND 1<<16 THEN
PROCwindow_close(TBoxWindow%)
PROCmenu_set_state(MainMenu%,6,0,0)
ELSE
PROCopen_window(TBoxWindow%)
PROCmenu_set_state(MainMenu%,6,1,0)
ENDIF
WHEN &1A1
!b%=enter%
SYS "Wimp_GetWindowState",,b%
IF b%!32 AND 1<<16 THEN
PROCwindow_close(enter%)
PROCmenu_set_state(MainMenu%,7,0,0)
ELSE
!b%=enter%
SYS "Wimp_GetWindowState",,b%
!q%=MainWindow%
SYS "Wimp_GetWindowState",,q%
b%!4=q%!4+42
IF q%!8>236 THENover%=0
IF q%!8<=236 AND b%!8>0 THENover%=236-b%!8
IF q%!8<=0 THENover%=236
b%!16=q%!8-44+over%
b%!8=q%!8-206+over%
b%!12=q%!4+1238
b%!20=0
b%!24=0
b%!28=TBarWindow%
SYS "Wimp_OpenWindow",,b%
PROCicon_put_caret_at_end(enter%,6)
PROCmenu_set_state(MainMenu%,7,1,0)
ENDIF
WHEN &1A2
IF FNmenu_state(MainMenu%,8) THEN
!b%=TBarWindow%
SYS "Wimp_GetWindowState",,b%
!q%=MainWindow%
SYS "Wimp_GetWindowState",,q%
b%!4=q%!4
b%!16=q%!16
b%!8=q%!16-36
b%!12=q%!12
b%!20=0
b%!24=0
b%!28=q%!28
SYS "Wimp_OpenWindow",,b%
PROCmenu_set_state(MainMenu%,8,0,0)
ELSE
!b%=TBarWindow%
SYS "Wimp_GetWindowState",,b%
b%!4=q%!4
b%!16=q%!16
b%!8=q%!16-136
b%!12=q%!12
b%!20=0
b%!24=0
b%!28=q%!28
SYS "Wimp_OpenWindow",,b%
PROCmenu_set_state(MainMenu%,8,1,0)
ENDIF
PROCredraw_main
PROCset_extent(0)
WHEN &182,&1CD :PROCaddval(2)
WHEN &192,&1DD :PROCaddval(1)
WHEN &183      :IF F3_qsave% THEN
PROCquick_save(file%)
ELSE
PROCwindow_open_as_menu_at(save%,!b%,b%!4)
ENDIF
WHEN &193      :PROCwindow_open_as_menu_at(text%,!b%,b%!4)
WHEN &1A3      :PROCwindow_open_as_menu_at(csv%,!b%,b%!4)
WHEN &184      :PROCwindow_open_as_menu_at(find%,!b%,b%!4)
WHEN &194      :PROCwindow_open_as_menu_at(SLineWindow%,!b%,b%!4)
WHEN &185,24   :PROCdelete
WHEN &195      :PROCremove
WHEN &186      :PROCaddval(3)
WHEN &187,3    :PROCcopy
WHEN &197      :PROCcurrent
WHEN &188,22   :PROCcut
WHEN &189      :PROCclip_clear
WHEN &18C,&19C
IF whan%=enter%
CASE icon% OF
WHEN 7  :PROCicon_put_caret_at_end(enter%,6)
WHEN 8  :PROCicon_put_caret_at_end(enter%,7)
WHEN 12 :PROCicon_put_caret_at_end(enter%,11)
WHEN 13 :PROCicon_put_caret_at_end(enter%,12)
ENDCASE
ENDIF
WHEN &18D,&19D
IF whan%=enter%
CASE icon% OF
WHEN 6  :PROCicon_put_caret_at_end(enter%,7)
WHEN 7  :PROCicon_put_caret_at_end(enter%,8)
WHEN 11 :PROCicon_put_caret_at_end(enter%,12)
WHEN 12 :PROCicon_put_caret_at_end(enter%,13)
ENDCASE
ENDIF
WHEN &1AC,&1BC :
IF whan%=enter%
CASE icon% OF
WHEN 7,8   :PROCicon_put_caret_at_end(enter%,6)
WHEN 12,13 :PROCicon_put_caret_at_end(enter%,11)
ENDCASE
ENDIF
WHEN &1AD,&1BD :
IF whan%=enter%
CASE icon% OF
WHEN 6,7   :PROCicon_put_caret_at_end(enter%,8)
WHEN 11,12 :PROCicon_put_caret_at_end(enter%,13)
ENDCASE
ENDIF
WHEN &1A9 :PROCclear
WHEN &1CA :PROCwindow_open_as_menu_at(GotoWindow%,!b%,b%!4)
WHEN &1CB :PROCwindow_open_as_menu_at(HeadWindow%,!b%,b%!4)
WHEN &18E
CASE whan% OF
WHEN enter% :PROCline_down
WHEN find%
CASE icon% OF
WHEN 1 :PROCicon_put_caret_at_end(find%,9)
WHEN 9 :PROCicon_put_caret_at_end(find%,1)
ENDCASE
WHEN text%
CASE icon% OF
WHEN 1  :PROCicon_put_caret_at_end(text%,10)
WHEN 10 :PROCicon_put_caret_at_end(text%,11)
WHEN 11 :PROCicon_put_caret_at_end(text%,1)
ENDCASE
WHEN csv%
CASE icon% OF
WHEN 1  :PROCicon_put_caret_at_end(csv%,10)
WHEN 10 :PROCicon_put_caret_at_end(csv%,11)
WHEN 11 :PROCicon_put_caret_at_end(csv%,1)
ENDCASE
WHEN print%
CASE icon% OF
WHEN 7 :PROCicon_put_caret_at_end(print%,8)
WHEN 8 :PROCicon_put_caret_at_end(print%,7)
ENDCASE
WHEN printset%
CASE icon% OF
WHEN 1 :PROCicon_put_caret_at_end(printset%,12)
WHEN 12,13,14,15,16 :PROCicon_put_caret_at_end(printset%,icon%+1)
WHEN 17 :PROCicon_put_caret_at_end(printset%,1)
ENDCASE
WHEN prefspane%
CASE icon% OF
WHEN 23 :PROCicon_put_caret_at_end(prefspane%,25)
WHEN 25 :PROCicon_put_caret_at_end(prefspane%,27)
WHEN 27 :PROCicon_put_caret_at_end(prefspane%,29)
WHEN 29 :PROCicon_put_caret_at_end(prefspane%,23)
ENDCASE
ENDCASE
WHEN &18F
CASE whan% OF
WHEN enter% :PROCline_up
WHEN find%
CASE icon% OF
WHEN 1 :PROCicon_put_caret_at_end(find%,9)
WHEN 9 :PROCicon_put_caret_at_end(find%,1)
ENDCASE
WHEN text%
CASE icon% OF
WHEN 1  :PROCicon_put_caret_at_end(text%,11)
WHEN 10 :PROCicon_put_caret_at_end(text%,1)
WHEN 11 :PROCicon_put_caret_at_end(text%,10)
ENDCASE
WHEN csv%
CASE icon% OF
WHEN 1  :PROCicon_put_caret_at_end(csv%,11)
WHEN 10 :PROCicon_put_caret_at_end(csv%,1)
WHEN 11 :PROCicon_put_caret_at_end(csv%,10)
ENDCASE
WHEN print%
CASE icon% OF
WHEN 7 :PROCicon_put_caret_at_end(print%,8)
WHEN 8 :PROCicon_put_caret_at_end(print%,7)
ENDCASE
WHEN printset%
CASE icon% OF
WHEN 1 :PROCicon_put_caret_at_end(printset%,17)
WHEN 12 :PROCicon_put_caret_at_end(printset%,1)
WHEN 13,14,15,16,17 :PROCicon_put_caret_at_end(printset%,icon%-1)
ENDCASE
WHEN prefspane%
CASE icon% OF
WHEN 23 :PROCicon_put_caret_at_end(prefspane%,29)
WHEN 25 :PROCicon_put_caret_at_end(prefspane%,23)
WHEN 27 :PROCicon_put_caret_at_end(prefspane%,25)
WHEN 29 :PROCicon_put_caret_at_end(prefspane%,27)
ENDCASE
ENDCASE
WHEN &19E :PROCpage_down
WHEN &19F :PROCpage_up
WHEN &1AE :PROCpage_end
WHEN &1AF :PROCpage_home
OTHERWISE :SYS "Wimp_ProcessKey",key%
ENDCASE
ENDPROC
:


REM Process incoming user messages.
REM
REM \param block%		The user message block.
:
DEF PROCuser_message(block%)
CASE block%!16 OF
	WHEN 0		: Quit%=TRUE
	WHEN 2		: PROCsave
	WHEN 3		: PROCdataload
	WHEN 5		: PROCquick_load
	WHEN 8		: PROCpre_quit
	WHEN 10		: PROCsave_desktop
	WHEN &502	: PROCsend_interactive_help(block%)
	WHEN &400C1	: PROCget_mode_vars
	WHEN &4E383	: PROCurl_bounce(block%)
ENDCASE
ENDPROC
:
DEF PROCaddval(opp%)
IF opp%<>0 AND select%=0 THEN PROCwimperror_report(FNmessage_lookup("Sel4"), 1, 1, ""):ENDPROC
LOCAL push%,text$,credit%,payment%
IF opp%<>3 AND entries%>1000 THEN PROCwimperror_report(FNmessage_lookup("Val2"), 1, 1, ""):ENDPROC
IF FNicon_selected(enter%,0) THEN
text$=$string%
credit%=FNval($cred%)
payment%=FNval($pay%)
ELSE
text$=$clip_string%
credit%=FNval($clip_cred%)
payment%=FNval($clip_pay%)
ENDIF
CASE opp% OF
WHEN 3
IF select%<>0 THEN
text$(select%)=text$
credit%(select%)=credit%
payment%(select%)=payment%
ENDIF
WHEN 0
text$(entries%)=text$
credit%(entries%)=credit%
payment%(entries%)=payment%
entries%+=1
WHEN 1,2
SYS "Hourglass_On"
IF opp%=2 THEN
FOR push%=entries%+2 TO select%+2 STEP -1
text$(push%)=text$(push%-1)
credit%(push%)=credit%(push%-1)
payment%(push%)=payment%(push%-1)
balance%(push%)=balance%(push%-1)
NEXT
text$(select%+1)=text$
credit%(select%+1)=credit%
payment%(select%+1)=payment%
entries%+=1
ELSE
IF select%<>0 THEN
FOR push%=entries%+2 TO select% STEP -1
text$(push%)=text$(push%-1)
credit%(push%)=credit%(push%-1)
payment%(push%)=payment%(push%-1)
balance%(push%)=balance%(push%-1)
NEXT
text$(select%)=text$
credit%(select%)=credit%
payment%(select%)=payment%
entries%+=1
select%+=1
ENDIF
ENDIF
SYS "Hourglass_Off"
ENDCASE
PROCrecalc
CASE opp% OF
WHEN 0 :PROCredraw_line(entries%-1)
WHEN 1 :PROCredraw_from(select%-1)
WHEN 2 :PROCredraw_from(select%+1)
WHEN 3 :PROCredraw_from(select%)
OTHERWISE
PROCredraw_main
ENDCASE
PROCupdate_tools(select%)
PROCredraw_enter
modified%=TRUE
PROCupdate_title
PROCset_extent(0)
IF opp%=0 THENPROCpage_end
ENDPROC
:
DEF PROCdelete
IF select%=0 THEN PROCwimperror_report(FNmessage_lookup("Sel0"), 1, 1, ""):ENDPROC
LOCAL push%
err% = FNwimperror_report(FNmessage_param_lookup("OK1", STR$(select%), "", "", ""), 4, 0, FNmessage_lookup("OK1B"))
IF err%=1 ENDPROC
SYS "Hourglass_On"
FOR push%=select% TO entries%-2
text$(push%)=text$(push%+1)
credit%(push%)=credit%(push%+1)
payment%(push%)=payment%(push%+1)
balance%(push%)=balance%(push%+1)
NEXT
text$(entries%-1)=""
credit%(entries%-1)=0
payment%(entries%-1)=0
balance%(entries%-1)=0
entries%-=1
SYS "Hourglass_Off"
PROCrecalc
entries%+=1
PROCredraw_from(select%)
entries%-=1
modified%=TRUE
PROCupdate_title
PROCupdate_tools(select%)
PROCredraw_enter
PROCclear_select
PROCset_extent(0)
ENDPROC
:
DEF PROCremove
IF select%=0 THEN PROCwimperror_report(FNmessage_lookup("Sel1"), 1, 1, ""):ENDPROC
err% = FNwimperror_report(FNmessage_param_lookup("OK2", STR$(select%), "", "", ""), 4, 0, FNmessage_lookup("OK2B"))
IF err%=1 ENDPROC
LOCAL push%
SYS "Hourglass_On"
balance%(0)=balance%(select%)
FOR push%=select%+1 TO entries%-1
text$(push%-select%)=text$(push%)
credit%(push%-select%)=credit%(push%)
payment%(push%-select%)=payment%(push%)
balance%(push%-select%)=balance%(push%)
NEXT
FOR push%=entries%-select% TO entries%-1
text$(push%)=""
credit%(push%)=0
payment%(push%)=0
balance%(push%)=0
NEXT
entries%-=select%
SYS "Hourglass_Off"
PROCclear_select
PROCrecalc
PROCredraw_main
modified%=TRUE
PROCupdate_title
PROCupdate_tools(select%)
PROCredraw_enter
PROCpage_home
PROCset_extent(0)
ENDPROC
:
DEF PROCcut
IF select%=0 THEN PROCwimperror_report(FNmessage_lookup("Sel2"), 1, 1, ""):ENDPROC
LOCAL push%
$clip_string%=text$(select%)
$clip_cred%=FNpoint(credit%(select%))
$clip_pay%=FNpoint(payment%(select%))
SYS "Hourglass_On"
FOR push%= select% TO entries%-2
text$(push%)=text$(push%+1)
credit%(push%)=credit%(push%+1)
payment%(push%)=payment%(push%+1)
balance%(push%)=balance%(push%+1)
NEXT
SYS "Hourglass_Off"
text$(entries%-1)=""
credit%(entries%-1)=0
payment%(entries%-1)=0
balance%(entries%-1)=0
entries%-=1
IF select%=entries% select%-=1
modified%=TRUE
PROCrecalc
IF set_clip% THEN
PROCicon_set_state(enter%,0,0,0,0)
PROCicon_set_state(enter%,1,1,0,0)
ENDIF
PROCupdate_tools(select%)
PROCupdate_title
PROCredraw_enter
entries%+=1
PROCredraw_from(select%)
entries%-=1
PROCclear_select
PROCset_extent(0)
ENDPROC
:
DEF PROCcopy
IF select%=0 THEN PROCwimperror_report(FNmessage_lookup("Sel3"), 1, 1, ""):ENDPROC
$clip_string%=text$(select%)
$clip_cred%=FNpoint(credit%(select%))
$clip_pay%=FNpoint(payment%(select%))
IF set_clip% THEN
PROCicon_set_state(enter%,0,0,0,0)
PROCicon_set_state(enter%,1,1,0,0)
ENDIF
PROCredraw_enter
ENDPROC
:
DEF PROCcurrent
$clip_string%=$string%
$clip_cred%=$cred%
$clip_pay%=$pay%
PROCredraw_enter
ENDPROC
:
DEF PROCnew_header
balance%(0)=FNval($header%)
PROCrecalc
PROCredraw_from(1)
PROCupdate_tools(select%)
PROCredraw_enter
PROCmenu_create(-1,0,0)
ENDPROC
:
DEF PROCselect(my%)
LOCAL into%,yoff%,row%,o%
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
yoff%=-b%!24
into%=b%!16-my%
IF FNmenu_state(MainMenu%,8) THENinto%-=100
row%=((yoff%+into%-12) DIV 32)
IF row%>=entries% OR row%=0 ENDPROC
o%=select%
select%=row%
PROCredraw_line(o%)
PROCredraw_line(select%)
PROCupdate_tools(select%)
PROCupdate_menu
PROCredraw_enter
ENDPROC
:
DEF PROCkey_select
LOCAL val%,o%
val%=VAL($selctline%)
IF val%<1 OR val%>=entries% THEN PROCwimperror_report(FNmessage_lookup("Sel5"), 1, 1, "") : ENDPROC
o%=select%
select%=val%
PROCredraw_line(o%)
PROCredraw_line(select%)
PROCupdate_tools(select%)
PROCupdate_menu
PROCredraw_enter
PROCmenu_create(-1,0,0)
$selctline%=""
ENDPROC
:
DEF PROCclear_select
LOCAL o%
o%=select%
select%=0
PROCredraw_line(o%)
PROCupdate_tools(select%)
PROCredraw_enter
PROCupdate_menu
ENDPROC
:
DEF PROCclip_clear
$clip_string%=""
$clip_cred%="0.00"
$clip_pay%="0.00"
PROCupdate_tools(select%)
PROCredraw_enter
PROCicon_set_state(enter%,0,1,0,0)
PROCicon_set_state(enter%,1,0,0,0)
ENDPROC
:
DEF PROCclear
err% = 0
IF modified% THEN
	err% = FNwimperror_report(FNmessage_lookup("OK3"), 4, 0, FNmessage_lookup("OK3B"))
	IF err% = 1 THEN ENDPROC
ENDIF

text$() = ""
credit%() = 0
payment%() = 0
balance%() = 0
entries% = 1
select% = 0
$file% = FNmessage_lookup("FileName")
$tfile% = FNmessage_lookup("TextName")
$cfile% = FNmessage_lookup("CsvName")
$string% = ""
$cred% = "0.00"
$pay% = "0.00"
modified% = FALSE

PROCupdate_tools(select%)
PROCupdate_menu
PROCupdate_title
PROCpage_home
PROCset_extent(TRUE)
PROCredraw_main
PROCredraw_enter
ENDPROC
:
DEF PROCrecalc
LOCAL calc%
SYS "Hourglass_On"
FOR calc%=1 TO entries%-1
balance%(calc%)=balance%(calc%-1)+credit%(calc%)-payment%(calc%)
NEXT
SYS "Hourglass_Off"
PROCcheck_size
ENDPROC
:
DEF PROCcheck_size
IF balance%(entries%-1)>2000000000 THEN
PROCwimperror_report(FNmessage_lookup("Val0"), 1, 1, "")
entries%-=1
text$(entries%)=""
credit%(entries%)=0
payment%(entries%)=0
balance%(entries%)=0
ENDIF
IF balance%(entries%-1)<-2000000000 THEN
PROCwimperror_report(FNmessage_lookup("Val1"), 1, 1, "")
entries%-=1
text$(entries%)=""
credit%(entries%)=0
payment%(entries%)=0
balance%(entries%)=0
ENDIF
ENDPROC
:
DEF PROCredraw
LOCAL left%,lmargin%,right%,top%,base%,visible%,relleft%,tool%,ts%
IF FNmenu_state(MainMenu%,8) tool%=4 :ts%=3 ELSEtool%=0 :ts%=0
SYS "Hourglass_On"
SYS "Wimp_RedrawWindow",,b% TO more%
ox%=b%!4-b%!20
oy%=b%!16-b%!24
WHILE more%
top%=((oy%-b%!40) DIV 32)-1
IF top%<0 top%=0
base%=(58+oy%-b%!32) DIV 32
left%=(ox%+b%!28) DIV 16
relleft%=(b%!28-ox%) DIV 16
IF left%<0 left%=0
IF relleft%<0 relleft%=0
right%=((ox%+b%!36) DIV 16)+1
visible%=((b%!36-b%!28) DIV 16)+2
i%=top%
font_l%=relleft%<<4
font_v%=visible%<<4
WHILE i%<=max%+3 AND i%<=base%
IF i%-ts%=select% AND i%-ts%<>0 THEN
SYS "Wimp_SetColour",colour_selection%
RECTANGLE FILL ox%,oy%-((i%)<<5)-40-tool%,1236,28
ELSE
IF balance%(i%-ts%)<0 THEN
SYS "Wimp_SetColour",colour_overdrawn%
RECTANGLE FILL ox%+962,oy%-((i%)<<5)-40-tool%,272,28
ENDIF
ENDIF
IF i%-ts%=select% AND i%-ts%<>0 THEN
IF use_font% THEN
SYS "Wimp_SetFontColours",,colour_selection%,FNinverse_colour(colour_selection%)
ELSE
SYS "Wimp_SetColour",FNinverse_colour(colour_selection%)
ENDIF
ELSE
IF use_font% THEN
SYS "Wimp_SetFontColours",,1,colour_numbers%
ELSE
SYS "Wimp_SetColour",colour_numbers%
ENDIF
ENDIF
IF relleft%<5 THEN
IF use_font% THEN
IF i%-ts%>0 AND relleft%<5 THEN
SYS "Font_SetFont",disp_font%
SYS "Font_Paint",,STR$(i%-ts%),16,ox%+74-FNtext_len(STR$(i%-ts%)),oy%-(i%<<5)-32-tool%
ENDIF
ELSE
MOVE ox%,oy%-(i%<<5)-12-tool%
IF i%-ts%>0 THEN
PRINT;FNtext(STR$(i%-ts%),4,FALSE)
ENDIF
ENDIF
ENDIF
IF i%-ts%>=entries% THEN
IF use_font% THEN
SYS "Wimp_SetFontColours",,1,colour_unused%
ELSE
SYS "Wimp_SetColour",colour_unused%
ENDIF
ELSE
IF i%-ts%=select% AND i%<>0 THEN
IF use_font% THEN
SYS "Wimp_SetFontColours",,colour_selection%,FNinverse_colour(colour_selection%)
ELSE
SYS "Wimp_SetColour",FNinverse_colour(colour_selection%)
ENDIF
ELSE
IF use_font% THEN
SYS "Wimp_SetFontColours",,1,colour_entries%
ELSE
SYS "Wimp_SetColour",colour_entries%
ENDIF
ENDIF
ENDIF
IF use_font% THEN
IF i%-ts%>0 THEN
SYS "Font_SetFont",disp_font%
IF (font_l%<100 AND font_l%+font_v%>100) OR (font_l%>100 AND font_l%<490) THEN
SYS "Font_Paint",,text$(i%-ts%),16,ox%+100,oy%-(i%<<5)-32-tool%
ENDIF
IF (font_l%<490 AND font_l%+font_v%>490) OR (font_l%>490 AND font_l%<686) THEN
text$=FNpoint(credit%(i%-ts%))
SYS "Font_Paint",,text$,16,ox%+686-FNtext_len(text$),oy%-(i%<<5)-32-tool%
ENDIF
IF (font_l%<686 AND font_l%+font_v%>686) OR (font_l%>686 AND font_l%<958) THEN
text$=FNpoint(payment%(i%-ts%))
SYS "Font_Paint",,text$,16,ox%+958-FNtext_len(text$),oy%-(i%<<5)-32-tool%
ENDIF
IF (font_l%<958 AND font_l%+font_v%>958) OR (font_l%>958 AND font_l%<1230) THEN
IF balance%(i%-ts%)<0 THEN
IF i%-ts%=select% THEN
SYS"Wimp_SetFontColours",,colour_selection%,colour_overdrawn%
ELSE
SYS"Wimp_SetFontColours",,colour_overdrawn%,colour_entries%
ENDIF
ENDIF
text$=FNpoint(balance%(i%-ts%))
SYS "Font_Paint",,text$,16,ox%+1230-FNtext_len(text$),oy%-(i%<<5)-32-tool%
ENDIF
ENDIF
ELSE
IF i%-ts%>0 THEN
line$=FNtext(text$(i%-ts%),20,TRUE)+"  "+FNtext(FNpoint(credit%(i%-ts%)),15,FALSE)+"  "+FNtext(FNpoint(payment%(i%-ts%)),15,FALSE)+"  "+FNtext(FNpoint(balance%(i%-ts%)),15,FALSE)
ELSE
line$=STRING$(80," ")
ENDIF
IF relleft%<6 THEN lmargin%=6 ELSE lmargin%=relleft%
MOVE ox%+(lmargin%<<4),oy%-(i%<<5)-12-tool%
PRINT;MID$(line$,lmargin%-5,visible%)
ENDIF
i%+=1
ENDWHILE
SYS "Wimp_GetRectangle",,b% TO more%
ENDWHILE
SYS "Hourglass_Off"
ENDPROC
:
DEF PROCset_extent(reset%)
LOCAL size%
!b%=0
size%=(entries%-1)*-32
IF size%>=-480 THENsize%=-480
size%-=40
IF FNmenu_state(MainMenu%,8) size%-=100
b%!4=size%
b%!8=1234
b%!12=0
SYS "Wimp_SetExtent",MainWindow%,b%
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
IF reset% THEN
b%!8=b%!16-520
IF FNmenu_state(MainMenu%,8) THENb%!8=b%!8-100
ENDIF
SYS "Wimp_OpenWindow",,b%
PROCopen_window(MainWindow%)
ENDPROC
:
DEF FNinverse_colour(c%)
IF c%<8 THEN=7-c%
IF c%>7 THEN=15-(c%-8)
:
DEF FNtext_len(t$)
SYS "Font_Converttopoints",,1280,40 TO ,xpts%,ypts%
SYS "Font_StringWidth",,t$,xpts%,ypts%,32,LEN(t$)+1 TO ,,xopt%,yopt%
SYS "Font_ConverttoOS",,xopt%,yopt% TO ,xo%
=xo%
:
DEF PROCredraw_line(line%)
y1%=-((line%+1)<<5)-8
y2%=-((line%)<<5)-8
IF FNmenu_state(MainMenu%,8) THENy1%-=100 :y2%-=100
SYS "Wimp_ForceRedraw",MainWindow%,0,y1%,1236,y2%
ENDPROC
:
DEF PROCredraw_main
LOCAL wth%,hht%
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
wth%=b%!12-b%!4
hht%=b%!16-b%!8
SYS "Wimp_ForceRedraw",MainWindow%,b%!20,b%!24-hht%,b%!20+wth%,b%!24
ENDPROC
:
DEF PROCredraw_enter
SYS "Wimp_ForceRedraw",enter%,234,-100,624,-52
SYS "Wimp_ForceRedraw",enter%,234,-172,624,-124
SYS "Wimp_ForceRedraw",enter%,652,-100,862,-52
SYS "Wimp_ForceRedraw",enter%,652,-172,862,-124
SYS "Wimp_ForceRedraw",enter%,890,-100,1100,-52
SYS "Wimp_ForceRedraw",enter%,890,-172,1100,-124
SYS "Wimp_ForceRedraw",enter%,1118,-100,1180,-52
SYS "Wimp_ForceRedraw",enter%,1118,-172,1180,-124
ENDPROC
:
DEF PROCredraw_from(line%)
LOCAL wth%,top%,base%
top%=-((line%)<<5)-8
base%=-((entries%)<<5)-8
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
IF FNmenu_state(MainMenu%,8) THENtop%-=100 :base%-=100
wth%=b%!12-b%!4
SYS "Wimp_ForceRedraw",MainWindow%,b%!20,base%,b%!20+wth%,top%
ENDPROC
:
DEF PROCupdate_title
IF $file% <> FNmessage_lookup("FileName") THEN
IF modified% $title%=$file%+" *" ELSE$title%=$file%
$fileinf_name%=$file%
ELSE
IF modified% THEN $title% = FNmessage_lookup("Untitled") + " *" ELSE $title% = FNmessage_lookup("Untitled")
$fileinf_name% = FNmessage_lookup("Untitled")
ENDIF
IF modified% THEN
	$safe% = FNmessage_lookup("Yes")
ELSE
	$safe% = FNmessage_lookup("No")
ENDIF
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
SYS "Wimp_ForceRedraw",-1,b%!4,b%!16,b%!12,b%!16+54
ENDPROC
:
DEF PROCupdate_tools(no%)
LOCAL size%
$string%=text$(no%)
$cred%=FNpoint(credit%(no%))
$pay%=FNpoint(payment%(no%))
$filesize%=STR$(entries%-1)
$header%=FNpoint(balance%(0))
$text2%=STR$(entries%-1)
$csv2%=STR$(entries%-1)
$print2%=STR$(entries%-1)
size%=entries%*15
size%+=entries%*2
size%+=SUMLEN(text$())
IF size%<10240 THEN
$units%="bytes"
ELSE
$units%="K"
size%=(size% DIV 1024)+1
ENDIF
$bfilesize%=STR$(size%)
IF modified% THEN
$safe%="Yes"
ELSE
$safe%="No"
ENDIF
SYS "Wimp_GetCaretPosition",,b%
IF !b%=enter% THENPROCicon_put_caret(!b%,b%!4,b%!20)
ENDPROC
:
DEF PROCupdate_menu
IF select%=0 THEN
PROCmenu_set_state(cmenu%,0,0,1)
PROCmenu_set_state(cmenu%,1,0,1)
PROCmenu_set_state(emenu%,1,0,1)
PROCmenu_set_state(emenu%,2,0,1)
PROCmenu_set_state(smenu%,1,0,1)
PROCmenu_set_state(smenu%,2,0,1)
PROCmenu_set_state(smenu%,3,0,1)
PROCmenu_set_state(smenu%,4,0,1)
PROCicon_set_state(TBoxWindow%,1,0,1,0)
PROCicon_set_state(TBoxWindow%,2,0,1,0)
PROCicon_set_state(TBoxWindow%,3,0,1,0)
PROCicon_set_state(TBoxWindow%,4,0,1,0)
PROCicon_set_state(TBarWindow%,2,0,1,0)
PROCicon_set_state(TBarWindow%,3,0,1,0)
PROCicon_set_state(TBarWindow%,4,0,1,0)
PROCicon_set_state(TBarWindow%,5,0,1,0)
PROCicon_set_state(TBarWindow%,6,0,1,0)
PROCicon_set_state(TBarWindow%,7,0,1,0)
PROCicon_set_state(TBarWindow%,8,0,1,0)
PROCicon_set_state(TBarWindow%,12,0,1,0)
ELSE
PROCmenu_set_state(cmenu%,0,0,0)
PROCmenu_set_state(cmenu%,1,0,0)
PROCmenu_set_state(emenu%,1,0,0)
PROCmenu_set_state(emenu%,2,0,0)
PROCmenu_set_state(smenu%,1,0,0)
PROCmenu_set_state(smenu%,2,0,0)
PROCmenu_set_state(smenu%,3,0,0)
PROCmenu_set_state(smenu%,4,0,0)
PROCicon_set_state(TBoxWindow%,1,0,0,0)
PROCicon_set_state(TBoxWindow%,2,0,0,0)
PROCicon_set_state(TBoxWindow%,3,0,0,0)
PROCicon_set_state(TBoxWindow%,4,0,0,0)
PROCicon_set_state(TBarWindow%,2,0,0,0)
PROCicon_set_state(TBarWindow%,3,0,0,0)
PROCicon_set_state(TBarWindow%,4,0,0,0)
PROCicon_set_state(TBarWindow%,5,0,0,0)
PROCicon_set_state(TBarWindow%,6,0,0,0)
PROCicon_set_state(TBarWindow%,7,0,0,0)
PROCicon_set_state(TBarWindow%,8,0,0,0)
PROCicon_set_state(TBarWindow%,12,0,0,0)
ENDIF
ENDPROC
:
DEF FNtext(t$,field%,left%)
LOCAL len%,pad%
len%=LENt$
IF len%>field% t$=LEFT$(t$,field%)
pad%=field%-len%
IF left% THEN
t$=t$+STRING$(pad%," ")
ELSE
t$=STRING$(pad%," ")+t$
ENDIF
=t$
:
DEF FNpoint(val%)
LOCAL len%,val$
val$=STR$val%
len%=LENval$
CASE len% OF
WHEN 1 :val$="0.0"+val$
WHEN 2 :val$="0."+val$
OTHERWISE
val$=LEFT$(val$,LENval$-2)+"."+RIGHT$(val$,2)
ENDCASE
=val$
:
DEF FNval(s$)
LOCAL point%,out$
point%=INSTR(s$,".")
IF point%<>0 THEN
out$=LEFT$(s$,point%-1)+MID$(s$,point%+1)
ELSE
out$=s$+"00"
ENDIF
=VALout$
:
DEF PROCfind(opt%,all%,rep%)
LOCAL search$,text$,done%,found%,flag%,o%
PROCmenu_create(-1,0,0)
PROCwindow_close(foundw%)
done%=FALSE :flag%=FALSE
IF entries%=1 THEN PROCwimperror_report(FNmessage_lookup("Fin1"), 1, 1, "") :ENDPROC
IF rep% THEN
IF insert_replace% THEN
text$(findno%)=LEFT$(LEFT$(text$(findno%),replace_pos%-1)+$replacetext%+MID$(text$(findno%),replace_pos%+replace_len%),20)
ELSE
text$(findno%)=LEFT$($replacetext%,20)
ENDIF
modified%=TRUE
PROCupdate_title
ENDIF
IF all% THENfindno%-=1
search$=$findtext%
IF FNicon_selected(find%,2)=0 search$=FNstring_to_upper(search$)
found%=FALSE
IF opt% THENfindno%=0
IF findno%>=entries%-1 THEN
$foundtext%=FNmessage_lookup("Fin2")
$foundline%="-"
PROCshade_button(foundw%,4)
PROCshade_button(foundw%,6)
PROCshade_button(foundw%,7)
PROCwindow_open_centred_at(foundw%,(width%/2)-334,456)
o%=select%
select%=0
PROCredraw_line(o%)
ENDIF
IF findno%>=entries%-1 ENDPROC
SYS "Hourglass_On"
IF FNicon_selected(find%,3) THEN
REPEAT
findno%+=1
text$=text$(findno%)
IF FNicon_selected(find%,2)=0 text$=FNstring_to_upper(text$)
IF INSTR(text$,search$) THEN
IF NOT all% THENfound%=TRUE
replace_pos%=INSTR(text$,search$)
replace_len%=LEN(search$)
insert_replace%=TRUE
IF all% THEN
text$(findno%)=LEFT$(LEFT$(text$(findno%),replace_pos%-1)+$replacetext%+MID$(text$(findno%),replace_pos%+replace_len%),20)
flag%=TRUE
IF NOT done% THEN
done%=TRUE
modified%=TRUE
PROCupdate_title
ENDIF
ENDIF
ENDIF
UNTIL findno%>=entries%-1 OR found%
ELSE
REPEAT
findno%+=1
text$=text$(findno%)
IF FNicon_selected(find%,2)=0 text$=FNstring_to_upper(text$)
IF text$=search$ THEN
IF NOT all% THENfound%=TRUE
insert_replace%=FALSE
IF all% THEN
text$(findno%)=LEFT$($replacetext%,20)
flag%=TRUE
IF NOT done% THEN
done%=TRUE
modified%=TRUE
PROCupdate_title
ENDIF
ENDIF
ENDIF
UNTIL findno%>=entries%-1 OR found%
ENDIF
SYS "Hourglass_Off"
IF found% THEN
o%=select%
select%=findno%
IF o%<>0 PROCredraw_line(o%)
PROCupdate_tools(select%)
PROCredraw_enter
$gotoline%=STR$(findno%)
PROCgoto
$foundtext%=text$(findno%)
$foundline%=STR$(findno%)
PROCwindow_open_centred_at(foundw%,(width%/2)-334,456)
PROCredraw_line(select%)
ELSE
IF NOT all% THEN
IF opt% THEN
	PROCwimperror_report(FNmessage_param_lookup("Fin0", $findtext%, "", "", ""), 1, 1, "")
	PROCpage_home
ELSE
$foundtext%=FNmessage_lookup("Fin2")
$foundline%="-"
PROCshade_button(foundw%,4)
PROCshade_button(foundw%,6)
PROCshade_button(foundw%,7)
PROCwindow_open_centred_at(foundw%,(width%/2)-334,456)
o%=select%
select%=0
PROCredraw_line(o%)
ENDIF
ELSE
PROCpage_home
IF NOT flag% THEN PROCwimperror_report(FNmessage_param_lookup("Fin0", $findtext%, "", "", ""), 1, 1, "")
ENDIF
ENDIF
IF all% THENPROCredraw_main
ENDPROC
:
DEF PROCgoto
LOCAL line%,offset%
line%=VAL($gotoline%)
IF line%>1000 line%=1000
offset%=-32*(line%-1)
IF FNmenu_state(MainMenu%,8) THENoffset%-=0
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
b%!24=offset%
SYS "Wimp_OpenWindow",,b%
$gotoline%=""
PROCmenu_create(-1,0,0)
ENDPROC
:
DEF PROCgoto_selection
LOCAL line%,offset%
line%=select%
IF line%>1000 line%=1000
offset%=-32*(line%-1)
IF FNmenu_state(MainMenu%,8) THENoffset%-=0
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
b%!24=offset%
SYS "Wimp_OpenWindow",,b%
$gotoline%=""
PROCmenu_create(-1,0,0)
ENDPROC
:
DEF PROCpage_up
LOCAL offset%,yh%
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
yh%=(b%!16-b%!8)-40
IF FNmenu_state(MainMenu%,8) THENyh%-=68
IF yh% MOD 32<>0 THENyh%-=(yh% MOD 32)
offset%=b%!24
offset%+=yh%
IF offset%>0 offset%=0
b%!24=offset%
SYS "Wimp_OpenWindow",,b%
ENDPROC
:
DEF PROCpage_down
LOCAL offset%,yh%
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
yh%=(b%!16-b%!8)-40
IF FNmenu_state(MainMenu%,8) THENyh%-=68
IF yh% MOD 32<>0 THENyh%-=(yh% MOD 32)
offset%=b%!24
offset%-=yh%
IF offset%<-31520 offset%=-31520
b%!24=offset%
SYS "Wimp_OpenWindow",,b%
ENDPROC
:
DEF PROCpage_home
!q%=MainWindow%
SYS "Wimp_GetWindowState",,q%
q%!24=0
SYS "Wimp_OpenWindow",,q%
ENDPROC
:
DEF PROCpage_end
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
b%!24=(entries%-2)*-32
SYS "Wimp_OpenWindow",,b%
ENDPROC
:
DEF PROCline_up
LOCAL offset%
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
offset%=b%!24
offset%+=32
IF offset%>0 offset%=0
b%!24=offset%
SYS "Wimp_OpenWindow",,b%
ENDPROC
:
DEF PROCline_down
LOCAL offset%
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
offset%=b%!24
offset%-=32
IF offset%<-31520 offset%=-31520
b%!24=offset%
SYS "Wimp_OpenWindow",,b%
ENDPROC
:
DEF PROCinit_load(file$)
b%!40=account_file%
$(b%+44)=file$+CHR$0
b%!20=-2
PROCdataload
ENDPROC
:
DEF PROCquick_load
IF b%!40<>account_file% ENDPROC
b%!12=b%!8
b%!16=4
SYS "Wimp_SendMessage",17,b%,b%!4
b%!20=-2
PROCdataload
ENDPROC
:
DEF PROCdataload
LOCAL tf$,csv%
csv%=FALSE
IF b%!20<>-2 AND b%!20<>MainWindow% THENENDPROC
IF b%!40<>account_file% AND b%!40<>csv_file% PROCwimperror_report(FNmessage_lookup("Fil2"), 1, 1, ""):ENDPROC
IF b%!20=MainWindow% AND b%!40=csv_file% THEN PROCwimperror_report(FNmessage_lookup("Fil4"), 1, 1, ""):ENDPROC
SYS "XOS_GenerateError", b%+44 TO tf$
IF b%!20=MainWindow% THENPROCopen_merge_window :mergef$=tf$ :ENDPROC
err%=0
IF b%!40=csv_file% THENcsv%=TRUE
IF modified% THEN err% = FNwimperror_report(FNmessage_lookup("OK4"), 4, 0, FNmessage_lookup("OK4B"))
IF err%=1 THENENDPROC
text$()=""
credit%()=0
payment%()=0
balance%()=0
entries%=1
select%=0
$file%=FNmessage_lookup("FileName")
$string%=""
$cred%="0.00"
$pay%="0.00"
modified%=FALSE
PROCupdate_tools(select%)
PROCupdate_title
!q%=MainWindow%
SYS "Wimp_GetWindowState",,q%
IF (q%!32 AND (1<<16)) THENPROCpage_home
PROCredraw_main
ENDIF
PROCredraw_enter
IF csv% THEN
	SYS "Wimp_GetPointerInfo",,q%
	PROCwindow_open_as_menu_at(loadcsv%, !q%, q%!4)
	csvf$=tf$
	ENDPROC
ENDIF
file$=tf$
c%=-1
SYS "Hourglass_On"
i%=OPENIN(file$)
REPEAT
c%+=1
INPUT#i%,text$(c%)
INPUT#i%,credit%(c%)
INPUT#i%,payment%(c%)
INPUT#i%,balance%(c%)
UNTIL EOF#i%
CLOSE#i%
SYS "Hourglass_Off"
$file%=file$
entries%=c%+1
modified%=FALSE
PROCupdate_title
PROCupdate_tools(select%)
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
PROCopen_window(MainWindow%)
PROCset_extent(TRUE)
PROCredraw_main
!b%=enter%
SYS "Wimp_GetWindowState",,b%
IF FNmenu_state(MainMenu%,7) THEN
PROCopen_window(enter%)
PROCicon_put_caret_at_end(enter%,6)
ENDIF
IF FNmenu_state(MainMenu%,6) PROCopen_window(TBoxWindow%)
PROCicon_set_state(enter%,0,1,0,0)
PROCicon_set_state(enter%,1,0,0,0)
ENDPROC
:
DEF PROCopen_merge_window
IF select%<>0 THEN
PROCclear_button(merge%,2)
PROCclear_button(merge%,3)
ELSE
PROCshade_button(merge%,2)
PROCshade_button(merge%,3)
ENDIF
SYS "Wimp_GetPointerInfo",,q%
PROCwindow_open_as_menu_at(merge%, !q%, q%!4)
ENDPROC
:
DEF PROCmerge_files(method%)
LOCAL finished%,s$,s%,c%,push%
finished%=TRUE
SYS "Hourglass_On"
i%=OPENIN(mergef$)
INPUT#i%,s$
INPUT#i%,s%
INPUT#i%,s%
INPUT#i%,s%
CASE method% OF
WHEN 0
REPEAT
INPUT#i%,text$(entries%)
INPUT#i%,credit%(entries%)
INPUT#i%,payment%(entries%)
INPUT#i%,balance%(entries%)
entries%+=1
UNTIL EOF#i% OR entries%>max%
WHEN 1,2
c%=select%
IF method%=2 THENc%+=1
REPEAT
FOR push%=entries% TO c%+1 STEP -1
text$(push%)=text$(push%-1)
credit%(push%)=credit%(push%-1)
payment%(push%)=payment%(push%-1)
balance%(push%)=balance%(push%-1)
NEXT push%
INPUT#i%,text$(c%)
INPUT#i%,credit%(c%)
INPUT#i%,payment%(c%)
INPUT#i%,balance%(c%)
entries%+=1 :c%+=1
UNTIL EOF#i% OR entries%>max%
ENDCASE
IF NOT EOF#i% THENfinished%=FALSE
CLOSE#i%
SYS "Hourglass_Off"
IF NOT finished% THEN err% = PROCwimperror_report(FNmessage_lookup("Mer0"), 0, 1, "")
PROCrecalc
PROCmenu_create(-1,0,0)
modified%=TRUE
PROCupdate_title
PROCupdate_tools(select%)
PROCredraw_main
PROCset_extent(0)
ENDPROC
:
DEF PROCcsv_columns(icon%,dir%)
LOCAL val%
CASE icon% OF
WHEN 0 :val%=VAL(MID$($cl_desc%,8))
WHEN 1 :val%=VAL(MID$($cl_cred%,8))
WHEN 2 :val%=VAL(MID$($cl_pay%,8))
ENDCASE
IF val%>0 AND val%<100 val%+=dir%
IF val%=0 THENval%=1
IF val%=100 THENval%=100
CASE icon% OF
WHEN 0 :$cl_desc%="Column "+STR$(val%)
PROCredraw_part(loadcsv%,282,-108,170,48)
WHEN 1 :$cl_cred%="Column "+STR$(val%)
PROCredraw_part(loadcsv%,282,-168,170,48)
WHEN 2 :$cl_pay%="Column "+STR$(val%)
PROCredraw_part(loadcsv%,282,-228,170,48)
ENDCASE
ENDPROC
:
DEF PROCcsv_load
LOCAL finished%,s$,line$,d_off%,c_off%,p_off%,failed%
finished%=TRUE :failed%=FALSE
SYS "Hourglass_On"
d_off%=VAL(MID$($cl_desc%,8))
c_off%=VAL(MID$($cl_cred%,8))
p_off%=VAL(MID$($cl_pay%,8))
i%=OPENIN(csvf$)
entries%=1
REPEAT
line$=GET$#i%
IF LEFT$(line$,1)<>";" AND LEFT$(line$,1)<>"|" AND line$<>"" THEN
text$(entries%)=LEFT$(FNcsv_field(line$,d_off%,failed%),20)
credit%(entries%)=FNval(FNcsv_field(line$,c_off%,failed%))
payment%(entries%)=FNval(FNcsv_field(line$,p_off%,failed%))
entries%+=1
ENDIF
UNTIL EOF#i% OR entries%>max% OR failed%
IF NOT EOF#i% AND NOT failed% THENfinished%=FALSE
CLOSE#i%
SYS "Hourglass_Off"
IF NOT finished% THEN PROCwimperror_report(FNmessage_lookup("Csv0"), 0, 1, "")
IF failed% THEN PROCwimperror_report(FNmessage_lookup("Csv1"), 0, 1, "")
$file%=csvf$
PROCrecalc
modified%=FALSE
PROCupdate_title
PROCupdate_tools(select%)
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
PROCopen_window(MainWindow%)
PROCredraw_main
PROCset_extent(TRUE)
!b%=enter%
SYS "Wimp_GetWindowState",,b%
IF FNmenu_state(MainMenu%,7) THEN
PROCopen_window(enter%)
PROCicon_put_caret_at_end(enter%,6)
ENDIF
IF FNmenu_state(MainMenu%,6) PROCopen_window(TBoxWindow%)
PROCmenu_create(-1,0,0)
ENDPROC
:
DEF FNcsv_field(s$,off%,RETURN e%)
LOCAL in%,f%,len%
f%=1 :in%=-1
IF off%>1 THEN
REPEAT
in%=INSTR(s$,",",in%+1)
f%+=1
UNTIL f%=off% OR in%=0
ENDIF
len%=INSTR(s$,",",in%+1)-in%
s$=MID$(s$,in%+1,len%-1)
IF in%=0 AND f%<>1 e%=TRUE
=s$
:
DEF PROCsave
CASE savetype% OF
WHEN account_file%
$file% = FNstring_read(b%+44)
PROCdatasave
WHEN text_file%
$tfile% = FNstring_read(b%+44)
PROCtextsave
WHEN csv_file%
$cfile% = FNstring_read(b%+44)
PROCcsvsave
ENDCASE
b%!12=b%!8
b%!16=3 :!b%=256
SYS "Wimp_SendMessage",18,b%,b%!20,b%!24
ENDPROC
ENDPROC
:
DEF PROCstart_save(len%,type%,text%)
IF drag_stop%<>-1 SYS drag_stop%
SYS "Wimp_GetPointerInfo",,q%
q%!20=q%!12
q%!24=q%!16
q%!28=!q%
q%!32=q%!4
q%!36=len%
!q%=64
q%!12=0
q%!16=1
q%!40=type%
$(q%+44)=FNstring_leafname($text%)
SYS "Wimp_SendMessage",18,q%,q%!20,q%!24
ENDPROC
:
DEF PROCdrag_box(whan%)
LOCAL ox%,oy%,spr$
!q%=whan%
SYS "Wimp_GetWindowState",,q%
ox%=q%!4-q%!20
oy%=q%!16-q%!24
q%!4=0
SYS "Wimp_GetIconState",,q%
CASE whan% OF
WHEN save% :spr$="file_1ac"
WHEN text% :spr$="file_fff"
WHEN csv% :spr$="file_dfe"
ENDCASE
q%!4=5
q%!8=ox%+q%!8
q%!12=oy%+q%!12
q%!16=ox%+q%!16
q%!20=oy%+q%!20
q%!24=0
q%!28=0
q%!32=&7FFFFFFF
q%!36=&7FFFFFFF
IF drag_start%=-1 THEN
SYS "Wimp_DragBox",,q%
ELSE
SYS drag_start%,%11000101,1,spr$,q%+8,q%+24
ENDIF
ENDPROC
:
DEF PROCquick_save(ptr%)
IF INSTR($ptr%,".") THEN
CASE ptr% OF
WHEN file% :PROCdatasave
WHEN tfile% :PROCtextsave
WHEN cfile% :PROCcsvsave
ENDCASE
ELSE
PROCwimperror_report(FNmessage_lookup("Fil0"), 0, 1, "")
ENDIF
ENDPROC
:
DEF PROCdatasave
LOCAL c%
SYS "Hourglass_On"
o%=OPENOUT($file%)
FOR c%=0 TO entries%-1
PRINT#o%,text$(c%)
PRINT#o%,credit%(c%)
PRINT#o%,payment%(c%)
PRINT#o%,balance%(c%)
NEXT
CLOSE#o%
SYS "OS_File",&12,$file%,account_file%
SYS "Hourglass_Off"
modified%=FALSE
PROCupdate_title
PROCmenu_create(-1,0,0)
ENDPROC
:
DEF PROCtextsave
LOCAL c%,percent%
IF VAL($text1%)=VAL($text2%) PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Exp0"), 0, 1, ""):ENDPROC
IF VAL($text1%)>VAL($text2%) PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Exp1"), 0, 1, ""):ENDPROC
IF VAL($text1%)>=entries% OR VAL($text2%)>=entries% PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Exp2"), 0, 1, ""):ENDPROC
IF VAL($text1%)<=0 OR VAL($text2%)<=0 PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Exp3"), 0, 1, ""):ENDPROC
SYS "Hourglass_On"
o%=OPENOUT($tfile%)
IF FNicon_selected(text%,12) THEN
BPUT#o%,"      "+FNtext("Date: "+LEFT$(TIME$,15),34,TRUE);
BPUT#o%,FNtext("File: "+LEFT$(FNstring_leafname($file%),LEN(FNstring_leafname($file%))-1),32,FALSE)
BPUT#o%," "
ENDIF
IF FNicon_selected(text%,13) THEN
BPUT#o%,FNtext("No.",4,FALSE)+"  ";
BPUT#o%,FNtext("Description",20,TRUE)+"  ";
BPUT#o%,FNtext("Credits",15,FALSE)+"  ";
BPUT#o%,FNtext("Payments",15,FALSE)+"  ";
BPUT#o%,FNtext("Balance",15,FALSE)+"  "
ENDIF
FOR c%=VAL($text1%) TO VAL($text2%)
BPUT#o%,FNtext(STR$c%,4,FALSE)+"  ";
BPUT#o%,FNtext(text$(c%),20,TRUE)+"  ";
BPUT#o%,FNtext(FNpoint(credit%(c%)),15,FALSE)+"  ";
BPUT#o%,FNtext(FNpoint(payment%(c%)),15,FALSE)+"  ";
BPUT#o%,FNtext(FNpoint(balance%(c%)),15,FALSE)
percent%=(c%-VAL($text1%))/(VAL($text2%)-VAL($text1%))*100
SYS "Hourglass_Percentage",percent%
NEXT
CLOSE#o%
SYS "OS_File",&12,$tfile%,text_file%
SYS "Hourglass_Off"
$tfile%=FNmessage_lookup("TextName")
PROCmenu_create(-1,0,0)
ENDPROC
:
DEF PROCcsvsave
LOCAL c%,percent%
IF VAL($csv1%)=VAL($csv2%) PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Exp0"), 0, 1, ""):ENDPROC
IF VAL($csv1%)>VAL($csv2%) PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Exp1"), 0, 1, ""):ENDPROC
IF VAL($csv1%)>=entries% OR VAL($csv2%)>=entries% PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Exp2"), 0, 1, ""):ENDPROC
IF VAL($csv1%)<=0 OR VAL($csv2%)<=0 PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Exp3"), 0, 1, ""):ENDPROC
SYS "Hourglass_On"
o%=OPENOUT($cfile%)
IF FNicon_selected(csv%,12) THEN
BPUT#o%,"No.,";
BPUT#o%,"Description,";
BPUT#o%,"Credits,";
BPUT#o%,"Payments,";
BPUT#o%,"Balance"
ENDIF
FOR c%=VAL($csv1%) TO VAL($csv2%)
BPUT#o%,STR$(c%)+",";
BPUT#o%,text$(c%)+",";
BPUT#o%,FNpoint(credit%(c%))+",";
BPUT#o%,FNpoint(payment%(c%))+",";
BPUT#o%,FNpoint(balance%(c%))
percent%=(c%-VAL($csv1%))/(VAL($csv2%)-VAL($csv1%))*100
SYS "Hourglass_Percentage",percent%
NEXT
CLOSE#o%
SYS "OS_File",&12,$cfile%,csv_file%
SYS "Hourglass_Off"
$cfile%=FNmessage_lookup("CsvName")
PROCmenu_create(-1,0,0)
ENDPROC
:
DEF FNsaved
IF modified% = FALSE THEN =TRUE
oktoquit% = FNwimperror_report(FNmessage_lookup("OK5"), 4, 0, FNmessage_lookup("OK5B"))
IF oktoquit% = 1 THEN
	SYS "Wimp_GetPointerInfo",,q%
	PROCwindow_open_as_menu_at(save%, !q%, q%!4)
ENDIF
=(oktoquit%=0)
:
DEF PROCprint
LOCAL c%,percent%,s%,top_of_form%,current_line%
IF VAL($print1%)=VAL($print2%) PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Prn0"), 0, 1, ""):ENDPROC
IF VAL($print1%)>VAL($print2%) PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Prn1"), 0, 1, ""):ENDPROC
IF VAL($print1%)>=entries% OR VAL($print2%)>=entries% PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Prn2"), 0, 1, ""):ENDPROC
IF VAL($print1%)<=0 OR VAL($print2%)<=0 PROCmenu_create(-1,0,0):PROCwimperror_report(FNmessage_lookup("Prn3"), 0, 1, ""):ENDPROC
SYS &80140 TO,s%
SYS "Hourglass_On"
o%=OPENOUT("<Account$PrintFile>")
current_line%=pagelength%
top_of_form%=TRUE
FOR c%=VAL($print1%) TO VAL($print2%)
IF top_of_form% AND FNicon_selected(print%,9) THEN
BPUT#o%,"      "+FNtext("Date: "+LEFT$(TIME$,15),34,TRUE);
BPUT#o%,FNtext("File: "+LEFT$(FNstring_leafname($file%),LEN(FNstring_leafname($file%))-1),32,FALSE)+CHR$(13)
current_line%-=1
ENDIF
IF top_of_form% THEN
BPUT#o%,uline_on$+bold_on$;
BPUT#o%,STRING$(79," ");
BPUT#o%,uline_off$+bold_off$+line_feed$
current_line%-=1
ENDIF
IF top_of_form% AND FNicon_selected(print%,10) THEN
BPUT#o%,uline_on$+bold_on$;
BPUT#o%,FNtext("No.",4,FALSE)+" | ";
BPUT#o%,FNtext("Description",20,TRUE)+"  ";
BPUT#o%,FNtext("Credits",15,FALSE)+"  ";
BPUT#o%,FNtext("Payments",15,FALSE)+"  ";
BPUT#o%,FNtext("Balance",15,FALSE)+" ";
BPUT#o%,uline_off$+bold_off$+line_feed$
current_line%-=1
ENDIF
IF top_of_form% THENtop_of_form%=FALSE
IF current_line%=1 OR c%=VAL($print2%) THENBPUT#o%,uline_on$;
BPUT#o%,FNtext(STR$c%,4,FALSE)+" | ";
BPUT#o%,FNtext(text$(c%),20,TRUE)+"  ";
BPUT#o%,FNtext(FNpoint(credit%(c%)),15,FALSE)+"  ";
BPUT#o%,FNtext(FNpoint(payment%(c%)),15,FALSE)+"  ";
BPUT#o%,FNtext(FNpoint(balance%(c%)),15,FALSE)+" "+line_feed$
IF current_line%=1 OR c%=VAL($print2%) THENBPUT#o%,uline_off$
percent%=(c%-VAL($text1%))/(VAL($text2%)-VAL($text1%))*100
SYS "Hourglass_Percentage",percent%
current_line%-=1
IF current_line%=0 THEN
current_line%=pagelength%
top_of_form%=TRUE
IF form_feeds% THENBPUT#o%,form_feed$
ENDIF
NEXT
IF form_feeds% AND top_of_form%=FALSE THENBPUT#o%,form_feed$
CLOSE#o%
SYS "Hourglass_Off"
PROCwindow_close(print%)
PROCmenu_create(-1,0,0)
ENDPROC
:
DEF PROCload_printcodes
in%=OPENIN("<Account$Dir>.PrintCodes")
$page_length%=GET$#in%
form_feeds%=VAL(GET$#in%)
$line_feed%=GET$#in%
$form_feed%=GET$#in%
$bold_on%=GET$#in%
$bold_off%=GET$#in%
$uline_on%=GET$#in%
$uline_off%=GET$#in%
CLOSE#in%
PROCicon_set_state(printset%,3,form_feeds%,0,0)
pagelength%=VAL($page_length%)
line_feed$=FNtrans_string($line_feed%)
form_feed$=FNtrans_string($form_feed%)
bold_on$=FNtrans_string($bold_on%)
bold_off$=FNtrans_string($bold_off%)
uline_on$=FNtrans_string($uline_on%)
uline_off$=FNtrans_string($uline_off%)
ENDPROC
:
DEF PROCset_printcodes
IF FNicon_selected(printset%,3) THENform_feeds%=1 ELSEform_feeds%=0
pagelength%=VAL($page_length%)
line_feed$=FNtrans_string($line_feed%)
form_feed$=FNtrans_string($form_feed%)
bold_on$=FNtrans_string($bold_on%)
bold_off$=FNtrans_string($bold_off%)
uline_on$=FNtrans_string($uline_on%)
uline_off$=FNtrans_string($uline_off%)
PROCmenu_create(-1,0,0)
ENDPROC
:
DEF PROCsave_printcodes
IF FNicon_selected(printset%,3) THENform_feeds%=1 ELSEform_feeds%=0
pagelength%=VAL($page_length%)
line_feed$=FNtrans_string($line_feed%)
form_feed$=FNtrans_string($form_feed%)
bold_on$=FNtrans_string($bold_on%)
bold_off$=FNtrans_string($bold_off%)
uline_on$=FNtrans_string($uline_on%)
uline_off$=FNtrans_string($uline_off%)
out%=OPENOUT("<Account$Dir>.PrintCodes")
BPUT#out%,$page_length%
BPUT#out%,STR$(form_feeds%)
BPUT#out%,$line_feed%
BPUT#out%,$form_feed%
BPUT#out%,$bold_on%
BPUT#out%,$bold_off%
BPUT#out%,$uline_on%
BPUT#out%,$uline_off%
CLOSE#in%
PROCmenu_create(-1,0,0)
ENDPROC
:
DEF PROCcancel_printcodes
PROCicon_set_state(printset%,3,form_feeds%,0,0)
$page_length%=STR$(pagelength%)
$line_feed%=FNtrans_codes(line_feed$)
$form_feed%=FNtrans_codes(form_feed$)
$bold_on%=FNtrans_codes(bold_on$)
$bold_off%=FNtrans_codes(bold_off$)
$uline_on%=FNtrans_codes(uline_on$)
$uline_off%=FNtrans_codes(uline_off$)
PROCmenu_create(-1,0,0)
ENDPROC
:
DEF FNtrans_string(s$)
LOCAL o$,pos%
WHILE LEN(s$)>0
IF INSTR(s$,",") THEN
pos%=INSTR(s$,",")
o$+=CHR$(VAL(LEFT$(s$,pos%-1)))
s$=MID$(s$,pos%+1)
ELSE
o$+=CHR$(VAL(s$))
s$=""
ENDIF
ENDWHILE
=o$
:
DEF FNtrans_codes(s$)
LOCAL o$
WHILE LEN(s$)>0
o$+=STR$(ASC(LEFT$(s$,1)))+","
s$=MID$(s$,2)
ENDWHILE
=LEFT$(o$,LEN(o$)-1)
:
DEF PROCsave_prefs(save_prefs%)
LOCAL open%,oj%
oj%=join_main%
IF save_prefs% THENout%=OPENOUT("<Account$Dir>.Choices")
IF FNicon_selected(prefspane%,2) THEN
return_caret%=TRUE
IF save_prefs% THENPRINT#out%,TRUE
ELSE
return_caret%=FALSE
IF save_prefs% THENPRINT#out%,FALSE
ENDIF
IF FNicon_selected(prefspane%,3) THEN
set_clip%=TRUE
IF save_prefs% THENPRINT#out%,TRUE
ELSE
set_clip%=FALSE
IF save_prefs% THENPRINT#out%,FALSE
ENDIF
IF FNicon_selected(prefspane%,4) THEN
join_main%=TRUE
IF save_prefs% THENPRINT#out%,TRUE
ELSE
join_main%=FALSE
IF save_prefs% THENPRINT#out%,FALSE
ENDIF
IF FNicon_selected(prefspane%,7) THEN
open_tools%=TRUE
IF save_prefs% THENPRINT#out%,TRUE
ELSE
open_tools%=FALSE
IF save_prefs% THENPRINT#out%,FALSE
ENDIF
IF FNicon_selected(prefspane%,8) THEN
open_enter%=TRUE
IF save_prefs% THENPRINT#out%,TRUE
ELSE
open_enter%=FALSE
IF save_prefs% THENPRINT#out%,FALSE
ENDIF
IF FNicon_selected(prefspane%,12) THEN
date_format%=1
IF save_prefs% THENPRINT#out%,1
ENDIF
IF FNicon_selected(prefspane%,13) THEN
date_format%=2
IF save_prefs% THENPRINT#out%,2
ENDIF
IF FNicon_selected(prefspane%,14) THEN
date_format%=3
IF save_prefs% THENPRINT#out%,3
ENDIF
IF FNicon_selected(prefspane%,16) THEN
date_option%=1
IF save_prefs% THENPRINT#out%,1
ENDIF
IF FNicon_selected(prefspane%,17) THEN
date_option%=2
IF save_prefs% THENPRINT#out%,2
ENDIF
IF FNicon_selected(prefspane%,18) THEN
date_option%=3
IF save_prefs% THENPRINT#out%,3
ENDIF
IF FNicon_selected(prefspane%,19) THEN
date_option%=4
IF save_prefs% THENPRINT#out%,4
ENDIF
IF FNicon_selected(prefspane%,20) THEN
date_fit%=TRUE
IF save_prefs% THENPRINT#out%,TRUE
ELSE
date_fit%=FALSE
IF save_prefs% THENPRINT#out%,FALSE
ENDIF
IF FNsafe_colour($entry_c%) THEN
colour_entries%=VAL($entry_c%)
IF save_prefs% THENPRINT#out%,colour_entries%
ELSE
IF save_prefs% THENPRINT#out%,colour_entries%
ENDIF
IF FNsafe_colour($numb_c%) THEN
colour_numbers%=VAL($numb_c%)
IF save_prefs% THENPRINT#out%,colour_numbers%
ELSE
IF save_prefs% THENPRINT#out%,colour_numbers%
ENDIF
IF FNsafe_colour($sel_c%) THEN
colour_selection%=VAL($sel_c%)
IF save_prefs% THENPRINT#out%,colour_selection%
ELSE
IF save_prefs% THENPRINT#out%,colour_selection%
ENDIF
IF FNsafe_colour($unused_c%) THEN
colour_unused%=VAL($unused_c%)
IF save_prefs% THENPRINT#out%,colour_unused%
ELSE
IF save_prefs% THENPRINT#out%,colour_unused%
ENDIF
IF FNsafe_colour($overdrawn_c%) THEN
colour_overdrawn%=VAL($overdrawn_c%)
IF save_prefs% THENPRINT#out%,colour_overdrawn%
ELSE
IF save_prefs% THENPRINT#out%,colour_overdrawn%
ENDIF
IF FNicon_selected(prefspane%,33) THEN
F3_qsave%=TRUE
IF save_prefs% THENPRINT#out%,TRUE
ELSE
F3_qsave%=FALSE
IF save_prefs% THENPRINT#out%,FALSE
ENDIF
IF FNicon_selected(prefspane%,43) THEN
IF save_prefs% THENPRINT#out%,TRUE
open_toolbar%=TRUE
ELSE
open_toolbar%=FALSE
IF save_prefs% THENPRINT#out%,FALSE
ENDIF
IF FNicon_selected(prefspane%,53) THEN
IF save_prefs% THENPRINT#out%,TRUE
use_font%=TRUE
ELSE
use_font%=FALSE
IF save_prefs% THENPRINT#out%,FALSE
ENDIF
IF FNicon_selected(prefspane%,47) THEN
home_key%=1
IF save_prefs% THENPRINT#out%,1
ENDIF
IF FNicon_selected(prefspane%,48) THEN
home_key%=2
IF save_prefs% THENPRINT#out%,2
ENDIF
IF FNicon_selected(prefspane%,49) THEN
home_key%=3
IF save_prefs% THENPRINT#out%,3
ENDIF
IF save_prefs% THENCLOSE#out%
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
IF (b%!32 AND (1<<16)) THENopen%=TRUE ELSEopen%=FALSE
IF open% THENPROCwindow_close(enter%)
IF oj%<>join_main% THEN
IF join_main% THEN
enter%=enter1%
string%=string1%
cred%=cred1%
pay%=pay1%
clip_string%=clip_string1%
clip_cred%=clip_cred1%
clip_pay%=clip_pay1%
:
$string1%=$string2%
$cred1%=$cred2%
$pay1%=$pay2%
$clip_string1%=$clip_string2%
$clip_cred1%=$clip_cred2%
$clip_pay1%=$clip_pay2%
ELSE
enter%=enter2%
string%=string2%
cred%=cred2%
pay%=pay2%
clip_string%=clip_string2%
clip_cred%=clip_cred2%
clip_pay%=clip_pay2%
:
$string2%=$string1%
$cred2%=$cred1%
$pay2%=$pay1%
$clip_string2%=$clip_string1%
$clip_cred2%=$clip_cred1%
$clip_pay2%=$clip_pay1%
ENDIF
ENDIF
IF open_tools% THEN
PROCmenu_set_state(MainMenu%,6,1,0)
ELSE
PROCmenu_set_state(MainMenu%,6,0,0)
ENDIF
IF open_enter% THEN
PROCmenu_set_state(MainMenu%,7,1,0)
ELSE
PROCmenu_set_state(MainMenu%,7,0,0)
ENDIF
IF open_toolbar% THEN
PROCmenu_set_state(MainMenu%,8,1,0)
ELSE
PROCmenu_set_state(MainMenu%,8,0,0)
ENDIF
IF open% THENPROCopen_window(MainWindow%) :PROCopen_window(enter%)
PROCwindow_close(prefspane%)
PROCwindow_close(prefs%)
PROCredraw_main
ENDPROC
:
DEF PROCload_prefs(load%)
LOCAL loop%,open%,oj%
oj%=join_main%
IF load% THENin%=OPENIN("<Account$Dir>.Choices")
IF load% THENINPUT#in%,return_caret%
IF return_caret% THEN
PROCicon_set_state(prefspane%,2,1,0,0)
ELSE
PROCicon_set_state(prefspane%,2,0,0,0)
ENDIF
IF load% THENINPUT#in%,set_clip%
IF set_clip% THEN
PROCicon_set_state(prefspane%,3,1,0,0)
ELSE
PROCicon_set_state(prefspane%,3,0,0,0)
ENDIF
IF load% THENINPUT#in%,join_main%
IF join_main% THEN
PROCicon_set_state(prefspane%,4,1,0,0)
ELSE
PROCicon_set_state(prefspane%,4,0,0,0)
ENDIF
IF load% THENINPUT#in%,open_tools%
IF open_tools% THEN
PROCicon_set_state(prefspane%,7,1,0,0)
ELSE
PROCicon_set_state(prefspane%,7,0,0,0)
ENDIF
IF load% THENINPUT#in%,open_enter%
IF open_enter% THEN
PROCicon_set_state(prefspane%,8,1,0,0)
ELSE
PROCicon_set_state(prefspane%,8,0,0,0)
ENDIF
FOR loop%=12 TO 14
PROCicon_set_state(prefspane%,loop%,0,0,0)
NEXT loop%
IF load% THENINPUT#in%,date_format%
CASE date_format% OF
WHEN 1 :PROCicon_set_state(prefspane%,12,1,0,0)
WHEN 2 :PROCicon_set_state(prefspane%,13,1,0,0)
WHEN 3 :PROCicon_set_state(prefspane%,14,1,0,0)
ENDCASE
FOR loop%=16 TO 19
PROCicon_set_state(prefspane%,loop%,0,0,0)
NEXT loop%
IF load% THENINPUT#in%,date_option%
CASE date_option% OF
WHEN 1 :PROCicon_set_state(prefspane%,16,1,0,0)
WHEN 2 :PROCicon_set_state(prefspane%,17,1,0,0)
WHEN 3 :PROCicon_set_state(prefspane%,18,1,0,0)
WHEN 4 :PROCicon_set_state(prefspane%,19,1,0,0)
ENDCASE
IF load% THENINPUT#in%,date_fit%
IF date_fit% THEN
PROCicon_set_state(prefspane%,20,1,0,0)
ELSE
PROCicon_set_state(prefspane%,20,0,0,0)
ENDIF
IF load% THENINPUT#in%,colour_entries%
IF load% THENINPUT#in%,colour_numbers%
IF load% THENINPUT#in%,colour_selection%
IF load% THENINPUT#in%,colour_unused%
IF load% THENINPUT#in%,colour_overdrawn%
$entry_c%=STR$(colour_entries%)
$numb_c%=STR$(colour_numbers%)
$sel_c%=STR$(colour_selection%)
$unused_c%=STR$(colour_unused%)
$overdrawn_c%=STR$(colour_overdrawn%)
IF load% THENINPUT#in%,F3_qsave%
IF F3_qsave% THEN
PROCicon_set_state(prefspane%,33,1,0,0)
ELSE
PROCicon_set_state(prefspane%,33,0,0,0)
ENDIF
IF load% THENINPUT#in%,open_toolbar%
IF open_toolbar% THEN
PROCicon_set_state(prefspane%,43,1,0,0)
ELSE
PROCicon_set_state(prefspane%,43,0,0,0)
ENDIF
IF load% THENINPUT#in%,use_font%
IF use_font% THEN
PROCicon_set_state(prefspane%,53,1,0,0)
ELSE
PROCicon_set_state(prefspane%,53,0,0,0)
ENDIF
FOR loop%=47 TO 49
PROCicon_set_state(prefspane%,loop%,0,0,0)
NEXT loop%
IF load% THENINPUT#in%,home_key%
CASE home_key% OF
WHEN 1 :PROCicon_set_state(prefspane%,47,1,0,0)
WHEN 2 :PROCicon_set_state(prefspane%,48,1,0,0)
WHEN 3 :PROCicon_set_state(prefspane%,49,1,0,0)
ENDCASE
IF load% THENCLOSE#in%
!b%=MainWindow%
SYS "Wimp_GetWindowState",,b%
IF (b%!32 AND (1<<16)) THENopen%=TRUE ELSEopen%=FALSE
IF open% THENPROCwindow_close(enter%)
IF oj%<>join_main% THEN
IF join_main% THEN
enter%=enter1%
string%=string1%
cred%=cred1%
pay%=pay1%
clip_string%=clip_string1%
clip_cred%=clip_cred1%
clip_pay%=clip_pay1%
:
$string1%=$string2%
$cred1%=$cred2%
$pay1%=$pay2%
$clip_string1%=$clip_string2%
$clip_cred1%=$clip_cred2%
$clip_pay1%=$clip_pay2%
ELSE
enter%=enter2%
string%=string2%
cred%=cred2%
pay%=pay2%
clip_string%=clip_string2%
clip_cred%=clip_cred2%
clip_pay%=clip_pay2%
:
$string2%=$string1%
$cred2%=$cred1%
$pay2%=$pay1%
$clip_string2%=$clip_string1%
$clip_cred2%=$clip_cred1%
$clip_pay2%=$clip_pay1%
ENDIF
ENDIF
IF open_tools% THEN
PROCmenu_set_state(MainMenu%,6,1,0)
ELSE
PROCmenu_set_state(MainMenu%,6,0,0)
ENDIF
IF open_enter% THEN
PROCmenu_set_state(MainMenu%,7,1,0)
ELSE
PROCmenu_set_state(MainMenu%,7,0,0)
ENDIF
IF open_toolbar% THEN
PROCmenu_set_state(MainMenu%,8,1,0)
ELSE
PROCmenu_set_state(MainMenu%,8,0,0)
ENDIF
IF open% THENPROCopen_window(MainWindow%) :PROCopen_window(enter%)
PROCredraw_main
ENDPROC
:
DEF PROCload_dft_prefs(defs%)
LOCAL loop%,data%
IF defs% THEN
in%=OPENIN("<Account$Dir>.Defaults")
ELSE
in%=OPENIN("<Account$Dir>.Choices")
ENDIF
INPUT#in%,data%
IF data% THEN
PROCicon_set_state(prefspane%,2,1,0,0)
ELSE
PROCicon_set_state(prefspane%,2,0,0,0)
ENDIF
INPUT#in%,data%
IF data% THEN
PROCicon_set_state(prefspane%,3,1,0,0)
ELSE
PROCicon_set_state(prefspane%,3,0,0,0)
ENDIF
INPUT#in%,data%
IF data% THEN
PROCicon_set_state(prefspane%,4,1,0,0)
ELSE
PROCicon_set_state(prefspane%,4,0,0,0)
ENDIF
INPUT#in%,data%
IF data% THEN
PROCicon_set_state(prefspane%,7,1,0,0)
ELSE
PROCicon_set_state(prefspane%,7,0,0,0)
ENDIF
INPUT#in%,data%
IF data% THEN
PROCicon_set_state(prefspane%,8,1,0,0)
ELSE
PROCicon_set_state(prefspane%,8,0,0,0)
ENDIF
FOR loop%=12 TO 14
PROCicon_set_state(prefspane%,loop%,0,0,0)
NEXT loop%
INPUT#in%,data%
CASE data% OF
WHEN 1 :PROCicon_set_state(prefspane%,12,1,0,0)
WHEN 2 :PROCicon_set_state(prefspane%,13,1,0,0)
WHEN 3 :PROCicon_set_state(prefspane%,14,1,0,0)
ENDCASE
FOR loop%=16 TO 19
PROCicon_set_state(prefspane%,loop%,0,0,0)
NEXT loop%
INPUT#in%,data%
CASE data% OF
WHEN 1 :PROCicon_set_state(prefspane%,16,1,0,0)
WHEN 2 :PROCicon_set_state(prefspane%,17,1,0,0)
WHEN 3 :PROCicon_set_state(prefspane%,18,1,0,0)
WHEN 4 :PROCicon_set_state(prefspane%,19,1,0,0)
ENDCASE
INPUT#in%,data%
IF data% THEN
PROCicon_set_state(prefspane%,20,1,0,0)
ELSE
PROCicon_set_state(prefspane%,20,0,0,0)
ENDIF
INPUT#in%,data%
$entry_c%=STR$(data%)
INPUT#in%,data%
$numb_c%=STR$(data%)
INPUT#in%,data%
$sel_c%=STR$(data%)
INPUT#in%,data%
$unused_c%=STR$(data%)
INPUT#in%,data%
$overdrawn_c%=STR$(data%)
INPUT#in%,data%
PROCredraw_part(prefspane%,42,-1352,50,48)
PROCredraw_part(prefspane%,42,-1428,50,48)
PROCredraw_part(prefspane%,42,-1508,50,48)
PROCredraw_part(prefspane%,42,-1588,50,48)
IF data% THEN
PROCicon_set_state(prefspane%,33,1,0,0)
ELSE
PROCicon_set_state(prefspane%,33,0,0,0)
ENDIF
INPUT#in%,data%
IF data% THEN
PROCicon_set_state(prefspane%,43,1,0,0)
ELSE
PROCicon_set_state(prefspane%,43,0,0,0)
ENDIF
INPUT#in%,data%
IF data% THEN
PROCicon_set_state(prefspane%,53,1,0,0)
ELSE
PROCicon_set_state(prefspane%,53,0,0,0)
ENDIF
FOR loop%=47 TO 49
PROCicon_set_state(prefspane%,loop%,0,0,0)
NEXT loop%
INPUT#in%,data%
CASE data% OF
WHEN 1 :PROCicon_set_state(prefspane%,47,1,0,0)
WHEN 2 :PROCicon_set_state(prefspane%,48,1,0,0)
WHEN 3 :PROCicon_set_state(prefspane%,49,1,0,0)
ENDCASE
CLOSE#in%
ENDPROC
:
DEF FNsafe_colour(s$)
IF VAL(s$)>=0 AND VAL(s$)<=15 THEN=TRUE ELSE=FALSE
:
DEF PROCswap(window%,icon%,caret%)
LOCAL text$,length%,chr%
!b%=window%
b%!4=icon%
SYS "Wimp_GetIconState",,b%
text$=$(b%!28)
length%=(b%!36)-1
chr%=ASC(MID$(text$,caret%+1,1))
CASE TRUE OF
WHEN (chr%>=65 AND chr%<=90 ) :chr%=chr%+32
WHEN (chr%>=97 AND chr%<=122) :chr%=chr%-32
ENDCASE
MID$(text$,caret%+1,1)=CHR$(chr%)
$(b%!28)=text$
PROCicon_put_caret(window%,icon%,caret%+1)
PROCredraw_part(window%,b%!8+8,b%!12+8,b%!16-b%!8-16,b%!20-b%!12-16)
ENDPROC
:
DEF PROCdate(window%,icon%,caret%)
LOCAL text$,length%
IF window%<>enter% THENENDPROC
IF icon%<>6 AND icon%<>11 THENENDPROC
!b%=window%
b%!4=icon%
SYS "Wimp_GetIconState",,b%
text$=$(b%!28)
length%=(b%!36)-1
text$=FNadd_date(text$,length%,caret%,date_format%,date_option%,date_fit%)
$(b%!28)=text$
PROCredraw_part(window%,b%!8+2,b%!12+4,b%!16-b%!8-4,b%!20-b%!12-8)
ENDPROC
:
DEF PROCnew_date(window%,icon%)
LOCAL text$,length%,type%,flag%,pos%
flag%=TRUE
IF window%<>enter% THENENDPROC
IF icon%<>6 AND icon%<>11 THENENDPROC
!b%=window%
b%!4=icon%
SYS "Wimp_GetIconState",,b%
text$=$(b%!28)
length%=(b%!36)-1
IF INSTR(text$,"/") THEN
pos%=-1
REPEAT
pos%=INSTR(text$,"/",pos%+1)
UNTIL pos%=0 OR MID$(text$,pos%+3,1)="/"
IF pos%>0 THEN
pos%-=3
type%=1
flag%=FALSE
ENDIF
ENDIF
IF FNmonth_in(text$) AND flag% THEN
flag%=FALSE
pos%=FNmonth_in(text$)
IF VAL(MID$(text$,pos%+4,2))>31 THEN
pos%-=4
type%=2
ELSE
pos%-=1
type%=3
ENDIF
ENDIF
IF flag% THEN PROCwimperror_report(FNmessage_lookup("Dat0"), 0, 1, "") : ENDPROC
text$=FNadd_date(text$,length%,pos%,type%,2,0)
$(b%!28)=text$
PROCredraw_part(window%,b%!8+2,b%!12+4,b%!16-b%!8-4,b%!20-b%!12-8)
ENDPROC
:
DEF FNmonth_in(t$)
LOCAL pos%,loop%,flag%
flag%=0
FOR loop%=1 TO 36 STEP 3
pos%=INSTR(text$,MID$(months$,loop%,3)+" ")
IF pos% THENflag%=pos%
NEXT loop%
=flag%
:
DEF FNadd_date(t$,len%,offset%,format%,option%,fit%)
LOCAL date$,dl%
date$=FNget_date(format%)
dl%=LEN(date$)
CASE option% OF
WHEN 1
IF (offset%+dl%+1>len%) AND fit% THENoffset%-=(offset%+dl%-len%)
t$=LEFT$(t$,offset%)+date$+MID$(t$,offset%+1)
IF LEN(t$)>len% THENt$=LEFT$(t$,len%)
WHEN 2
IF (offset%+dl%+1>len%) AND fit% THENoffset%-=(offset%+dl%-len%)
MID$(t$,offset%+1,dl%)=date$
IF LEN(t$)>len% THENt$=LEFT$(t$,len%)
WHEN 3
IF LEN(t$)<(len%-dl%) THENt$+=STRING$((len%-dl%)-LEN(t$)," ")
t$+=date$
IF LEN(t$)>len% THENt$=RIGHT$(t$,len%)
WHEN 4
IF LEN(t$)<len% THENt$+=STRING$(len%-LEN(t$)," ")
MID$(t$,len%-dl%+1,dl%)=date$
IF LEN(t$)>len% THENt$=RIGHT$(t$,len%)
ENDCASE
=t$
:
DEF FNget_date(f%)
LOCAL d$,s$
s$=MID$(TIME$,5,11)
CASE f% OF
WHEN 1 :d$=LEFT$(s$,2)+"/"+FNstr(FNmonth(MID$(s$,4,3)))+"/"+RIGHT$(s$,2)
WHEN 2 :d$=LEFT$(s$,2)+" "+MID$(s$,4,3)+" "+RIGHT$(s$,2)
WHEN 3 :d$=MID$(s$,4,3)+" "+LEFT$(s$,2)+" "+RIGHT$(s$,2)
ENDCASE
=d$
:
DEF FNmonth(name$)
LOCAL index%
index%=INSTR(months$,name$)
IF index%>0 THENindex%=(index% DIV 3)+1
=index%
:
DEF FNstr(val%)
LOCAL s$
s$=STR$(val%)
IF LEN(s$)=1 THENs$="0"+s$
=s$


REM Initialise ourselves, internally and with the Wimp.
:
DEF PROCinitialise
LOCAL resources$, task_name$, task_sprite$

SYS "Hourglass_On"

REM Set up the quit flag and global data blocks.

Quit% = FALSE

DIM b% 4000, q% 255, a% 255

REM Locate the application resources.

resources$ = FNresources_find_territory_folder("<Account$Dir>.Resources")

REM Load the messages file.

PROCmessage_initialise(resources$ + "Messages")

task_name$ = FNmessage_lookup("TaskName")
task_sprite$ = FNmessage_lookup("TaskSpr")
PROCwimperror_initialise(task_name$, task_sprite$)

REM Initialise with the Wimp.

q%!0 = 2
q%!4 = 3
q%!8 = 5
q%!12 = 8
q%!16 = 10
q%!20 = &502
q%!24 = &400C1
q%!28 = 0

SYS "Wimp_Initialise", 310, &4B534154, task_name$, q%




RestartShutdown%=FALSE

max%=1000
modified%=FALSE
select%=0
entries%=1
months$="JanFebMarAprMayJunJulAugSepOctNovDec"
account_file%=&1AC
text_file%=&FFF
csv_file%=&DFE
join_main%=1
PROCget_mode_vars
DIM text$(max%),credit%(max%),payment%(max%),balance%(max%)



:
PROCintro
PROCdefine_windows(resources$)
PROCdefine_icons

REM Initialise and load the menu templates.

PROCmenu_initialise

b%!0 = InfoWindow%
b%!4 = InfoWindow%
b%!8 = fileinf%
b%!12 = find%
b%!16 = GotoWindow%
b%!20 = HeadWindow%
b%!24 = save%
b%!28 = text%
b%!32 = csv%
b%!36 = SLineWindow%
b%!40 = print%
b%!44 = printset%
PROCmenu_load_templates(resources$ + "Menus", b%)
IconbarMenu% = b%!0
MainMenu% = b%!4
REM gmenu% = b%!8
REM fmenu% = b%!12
emenu% = b%!16
smenu% = b%!20
cmenu% = b%!24
REM pmenu% = b%!28
ColourMenu% = b%!32

PROCupdate_menu
PROCload_printcodes
PROCload_prefs(TRUE)
PROCdrag_init

SYS "Hourglass_Off"

state%=INSTR(CommandLine$,"!RunImage")+11
CommandLine$=MID$(CommandLine$,state%)
IF CommandLine$<>"" PROCinit_load(CommandLine$)
IntroUpAt%=TIME
PROCopen_window(intro%)
ENDPROC
:
DEF PROCdrag_init
drag_start%=FNswi("DragASprite_Start")
drag_stop%=FNswi("DragASprite_Stop")
SYS "OS_Byte",161,28 TO ,,cmos
IF (cmos AND 2)=0 drag_start%=-1:drag_stop%=-1
ENDPROC
:
DEF FNswi(swi$)
SYS "XOS_SWINumberFromString",,swi$ TO swi;flag
IF (flag AND 1) swi=-1
=swi
:
DEF PROCget_mode_vars
LOCAL x%,y%,w%,h%
SYS "OS_ReadModeVariable",-1,4 TO ,,x%
SYS "OS_ReadModeVariable",-1,5 TO ,,y%
SYS "OS_ReadModeVariable",-1,11 TO ,,w%
SYS "OS_ReadModeVariable",-1,12 TO ,,h%
x%=x%*2
y%=y%*2
width%=(w%+1)*x%
height%=(h%+1)*y%
ENDPROC
:
DEF PROCsave_desktop
LOCAL len%
SYS "OS_ReadVarVal","Account$Dir",q%,256,0,0 TO ,,len%
q%?len%=13
BPUT#b%!20,"Run "+$q%
ENDPROC
:
DEF PROCpre_quit
IF NOT modified% THEN ENDPROC
!b%=20
b%!12=b%!8
b%!16=0
SYS "Wimp_SendMessage",19,b%
err% = FNwimperror_report(FNmessage_lookup("OK6"), 4, 0, FNmessage_lookup("OK6B"))
IF err%=0 THEN Quit%=TRUE : RestartShutdown%=TRUE
IF err%=1 THEN
	SYS "Wimp_GetPointerInfo",,q%
	PROCwindow_open_as_menu_at(save%, !q%, q%!4)
ENDIF
ENDPROC
:
DEF PROCintro
SYS "Font_FindFont",,"Homerton.Bold.Oblique",480,960 TO font1%
SYS "Font_FindFont",,"Homerton.Medium",320,320 TO font2%
SYS "Font_FindFont",,"Homerton.Medium.Oblique",320,320 TO font3%
SYS "Font_FindFont",,"Corpus.Medium",192,192 TO disp_font%
IntroDisplayTime%=100*8
ENDPROC
:
DEF PROCloose_fonts
SYS "Font_LoseFont",font1%
SYS "Font_LoseFont",font2%
SYS "Font_LoseFont",font3%
SYS "Font_LoseFont",disp_font%
ENDPROC
:
DEF PROCdefine_windows(resources$)
LOCAL sprite_area%, ind_data%, ind_size%

ind_size% = 7000
DIM ind_data% ind_size%

sprite_area% = FNwimpsprite_load_user_sprites("<Account$Dir>.Sprites")

PROCtemplate_open(resources$ + "Templates")

PROCtemplate_load("Main", b%, ind_data%, ind_size%, -1)
title% = b%!72
$title% = FNmessage_lookup("Untitled")
SYS "Wimp_CreateWindow",,b% TO MainWindow%
PROCtemplate_load("Tools", b%, ind_data%, ind_size%, -1)
b%!64=sprite_area%
SYS "Wimp_CreateWindow",,b% TO TBoxWindow%
PROCtemplate_load("Enter1", b%, ind_data%, ind_size%, -1)
b%!64=sprite_area%
SYS "Wimp_CreateWindow",,b% TO enter1%
PROCtemplate_load("Enter2", b%, ind_data%, ind_size%, -1)
b%!64=sprite_area%
SYS "Wimp_CreateWindow",,b% TO enter2%
PROCtemplate_load("TitlePane", b%, ind_data%, ind_size%, -1)
b%!64=sprite_area%
SYS "Wimp_CreateWindow",,b% TO TBarWindow%
PROCtemplate_load("Goto", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO GotoWindow%
PROCtemplate_load("Select", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO SLineWindow%
PROCtemplate_load("Header", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO HeadWindow%
PROCtemplate_load("Print", b%, ind_data%, ind_size%, -1)
b%!64=sprite_area%
SYS "Wimp_CreateWindow",,b% TO print%
PROCtemplate_load("ExpText", b%, ind_data%, ind_size%, -1)
b%!64=sprite_area%
SYS "Wimp_CreateWindow",,b% TO text%
PROCtemplate_load("ExpCSV", b%, ind_data%, ind_size%, -1)
b%!64=sprite_area%
SYS "Wimp_CreateWindow",,b% TO csv%
PROCtemplate_load("Save", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO save%
PROCtemplate_load("ProgInfo", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO InfoWindow%
PROCtemplate_load("Find", b%, ind_data%, ind_size%, -1)
b%!64=sprite_area%
SYS "Wimp_CreateWindow",,b% TO find%
PROCtemplate_load("Found", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO foundw%
PROCtemplate_load("PrintSetUp", b%, ind_data%, ind_size%, -1)
b%!64=sprite_area%
SYS "Wimp_CreateWindow",,b% TO printset%
PROCtemplate_load("Prefs", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO prefs%
PROCtemplate_load("PrefsPane", b%, ind_data%, ind_size%, -1)
b%!64=sprite_area%
SYS "Wimp_CreateWindow",,b% TO prefspane%
PROCtemplate_load("FileInfo", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO fileinf%
PROCtemplate_load("LoadCSV", b%, ind_data%, ind_size%, -1)
b%!64=sprite_area%
SYS "Wimp_CreateWindow",,b% TO loadcsv%
PROCtemplate_load("Merge", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO merge%
PROCtemplate_load("Announce", b%, ind_data%, ind_size%, -1)
b%!168+=(1<<6)+(font1%<<24)
b%!200+=(1<<6)+(font2%<<24)
b%!232+=(1<<6)+(font3%<<24)
SYS "Wimp_CreateWindow",,b% TO intro%

PROCtemplate_close
ENDPROC
:
DEF PROCdefine_icons
icon% = FNicon_create_standard_bar(-1, "!Accounts")
:
REM Info
$FNicon_indirection(InfoWindow%,4)=CHR$(169) + " Stephen Fryatt, 1992-" + MID$(build_date$, 8)
$FNicon_indirection(InfoWindow%,6)=build_version$+" ("+build_date$+")"
:
REM Intro
$FNicon_indirection(intro%,2)=FNmessage_param_lookup("Int0", build_version$, "", "", "")
$FNicon_indirection(intro%,3)=CHR$(169) + " Stephen Fryatt, 1992-" + MID$(build_date$, 8)
:
REM Enter1
string1%=FNicon_indirection(enter1%,6)
cred1%=FNicon_indirection(enter1%,7)
pay1%=FNicon_indirection(enter1%,8)
clip_string1%=FNicon_indirection(enter1%,11)
clip_cred1%=FNicon_indirection(enter1%,12)
clip_pay1%=FNicon_indirection(enter1%,13)
:
REM Enter 2
string2%=FNicon_indirection(enter2%,6)
cred2%=FNicon_indirection(enter2%,7)
pay2%=FNicon_indirection(enter2%,8)
clip_string2%=FNicon_indirection(enter2%,11)
clip_cred2%=FNicon_indirection(enter2%,12)
clip_pay2%=FNicon_indirection(enter2%,13)
:
REM File Info
fileinf_name%=FNicon_indirection(fileinf%,5)
safe%=FNicon_indirection(fileinf%,6)
filesize%=FNicon_indirection(fileinf%,7)
bfilesize%=FNicon_indirection(fileinf%,8)
units%=FNicon_indirection(fileinf%,4)
:
REM Goto
gotoline%=FNicon_indirection(GotoWindow%,0)
selctline%=FNicon_indirection(SLineWindow%,0)
:
REM Find
findtext%=FNicon_indirection(find%,1)
replacetext%=FNicon_indirection(find%,9)
:
REM Found
foundtext%=FNicon_indirection(foundw%,2)
foundline%=FNicon_indirection(foundw%,3)
:
REM New Header
header%=FNicon_indirection(HeadWindow%,1)
:
REM Save
file%=FNicon_indirection(save%,1)
$file%=FNmessage_lookup("FileName")
:
REM Export Text
tfile%=FNicon_indirection(text%,1)
$tfile%=FNmessage_lookup("TextName")
text1%=FNicon_indirection(text%,10)
text2%=FNicon_indirection(text%,11)
$text1%="1"
$text2%="1"
:
REM Export CSV
cfile%=FNicon_indirection(csv%,1)
$cfile%=FNmessage_lookup("CsvName")
csv1%=FNicon_indirection(csv%,10)
csv2%=FNicon_indirection(csv%,11)
$csv1%="1"
$csv2%="1"
:
REM Print
print1%=FNicon_indirection(print%,7)
print2%=FNicon_indirection(print%,8)
$print1%="1"
$print2%="1"
:
REM Print Set Up
page_length%=FNicon_indirection(printset%,1)
$page_length%="62"
form_feed%=FNicon_indirection(printset%,13)
line_feed%=FNicon_indirection(printset%,12)
bold_on%=FNicon_indirection(printset%,14)
bold_off%=FNicon_indirection(printset%,15)
uline_on%=FNicon_indirection(printset%,16)
uline_off%=FNicon_indirection(printset%,17)
:
REM Preferences
entry_c%=FNicon_indirection(prefspane%,23)
numb_c%=FNicon_indirection(prefspane%,25)
sel_c%=FNicon_indirection(prefspane%,27)
unused_c%=FNicon_indirection(prefspane%,29)
overdrawn_c%=FNicon_indirection(prefspane%,50)
$entry_c%="7"
$numb_c%="11"
$sel_c%="7"
$unused_c%="2"
$overdrawn_c%="11"
:
REM Load CSV
cl_desc%=FNicon_indirection(loadcsv%,0)
cl_cred%=FNicon_indirection(loadcsv%,1)
cl_pay%=FNicon_indirection(loadcsv%,2)
ENDPROC



REM Process requests for interactive help text.
REM
REM \param message%		Pointer to the Message_HelpRequest block.
:
DEF PROCsend_interactive_help(message%)
LOCAL root$, suffix$, found%, message$

REM Compare the window handle to the windows we know about, and find the
REM relevant token name.

CASE message%!32 OF
	WHEN -2			: root$ = "IconBar"
	WHEN GotoWindow%	: root$ = "Goto"
	WHEN HeadWindow%	: root$ = "Head"
	WHEN MainWindow%	: root$ = "Main"
	WHEN InfoWindow%	: root$ = "Info"
	WHEN SLineWindow%	: root$ = "SLine"
	WHEN TBoxWindow%	: root$ = "TBox"
	WHEN TBarWindow%	: root$ = "TBar"
REM	WHEN ChoicesWindow%	: root$ = "Choices"
	OTHERWISE		: root$ = ""
ENDCASE

REM Look up the help text, either as a window message or by checking the
REM menus that belong to us.

IF root$ <> "" THEN
	suffix$ = FNicon_validation_command(message%!32, message%!36, "N")

	REM If there's a name in the validation string, use that; otherwise
	REM check to see if there's a special case to set up.

	IF suffix$ <> "" THEN
		root$ += "." + suffix$
	ELSE
REM		IF message%!32 = MainWindow% AND message%!36 >= 0 AND message%!36 <= 26 THEN root$ += ".Grid"
	ENDIF
	found% = FNmessage_lookup_result("Help." + root$, message$)
ELSE
	SYS "Wimp_GetMenuState", 1, q%, message%!32, message%!36
	CASE FNmenu_current_handle OF
		WHEN MainMenu%		: root$ = "MainMenu." + RIGHT$("00" + STR$(!q%), 2)
		WHEN IconbarMenu%	: root$ = "IconBarMenu." + RIGHT$("00" + STR$(!q%), 2)
	ENDCASE
	
	IF root$ <> "" THEN found% = FNmessage_lookup_result("Help." + root$, message$)
ENDIF

REM If no message was found, give up now.
REM
REM \TODO -- Remove call to PROChelp once all help is handled via the new code.

IF NOT found% THEN PROChelp(message%!32, message%!36) : ENDPROC

REM If a message was found, send it back to the requestor.

message%!12 = message%!8
message%!16 = &503
$(message% + 20) = message$ + CHR$(0)
!message% = 24+(LEN(message$) AND &FFFFFC)
SYS "Wimp_SendMessage", 17, message%, message%!4
ENDPROC

DEF PROChelp(window%, icon%)
CASE window% OF
WHEN enter%
CASE icon% OF
WHEN 0
PROCsend_help(FNmessage_lookup("Hlp8"))
WHEN 1
PROCsend_help(FNmessage_lookup("Hlp9"))
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp10"))
WHEN 3
PROCsend_help(FNmessage_lookup("Hlp11"))
WHEN 4
PROCsend_help(FNmessage_lookup("Hlp12"))
WHEN 5
PROCsend_help(FNmessage_lookup("Hlp13"))
WHEN 6
PROCsend_help(FNmessage_lookup("Hlp14"))
WHEN 7
PROCsend_help(FNmessage_lookup("Hlp15"))
WHEN 8
PROCsend_help(FNmessage_lookup("Hlp16"))
WHEN 9
PROCsend_help(FNmessage_lookup("Hlp17"))
WHEN 10
PROCsend_help(FNmessage_lookup("Hlp18"))
WHEN 11
PROCsend_help(FNmessage_lookup("Hlp19"))
WHEN 12
PROCsend_help(FNmessage_lookup("Hlp20"))
WHEN 13
PROCsend_help(FNmessage_lookup("Hlp21"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp22"))
ENDCASE
WHEN find%
CASE icon% OF
WHEN 0
PROCsend_help(FNmessage_lookup("Hlp183"))
WHEN 1,10
PROCsend_help(FNmessage_lookup("Hlp26"))
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp27"))
WHEN 3
PROCsend_help(FNmessage_lookup("Hlp28"))
WHEN 4
PROCsend_help(FNmessage_lookup("Hlp29"))
WHEN 5
PROCsend_help(FNmessage_lookup("Hlp30"))
WHEN 6
PROCsend_help(FNmessage_lookup("Hlp31"))
WHEN 7
PROCsend_help(FNmessage_lookup("Hlp120"))
WHEN 8,9
PROCsend_help(FNmessage_lookup("Hlp121"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp32"))
ENDCASE
WHEN foundw%
CASE icon% OF
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp33"))
WHEN 3
PROCsend_help(FNmessage_lookup("Hlp34"))
WHEN 4
PROCsend_help(FNmessage_lookup("Hlp35"))
WHEN 5
PROCsend_help(FNmessage_lookup("Hlp36"))
WHEN 6
PROCsend_help(FNmessage_lookup("Hlp122"))
WHEN 7
PROCsend_help(FNmessage_lookup("Hlp123"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp37"))
ENDCASE
WHEN save%
CASE icon% OF
WHEN 0
PROCsend_help(FNmessage_lookup("Hlp42"))
WHEN 1
PROCsend_help(FNmessage_lookup("Hlp43"))
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp44"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp45"))
ENDCASE
WHEN text%
CASE icon% OF
WHEN 0
PROCsend_help(FNmessage_lookup("Hlp46"))
WHEN 1
PROCsend_help(FNmessage_lookup("Hlp47"))
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp48"))
WHEN 3
PROCsend_help(FNmessage_lookup("Hlp49"))
WHEN 4
PROCsend_help(FNmessage_lookup("Hlp50"))
WHEN 5
PROCsend_help(FNmessage_lookup("Hlp51"))
WHEN 6
PROCsend_help(FNmessage_lookup("Hlp52"))
WHEN 7
PROCsend_help(FNmessage_lookup("Hlp53"))
WHEN 8
PROCsend_help(FNmessage_lookup("Hlp54"))
WHEN 9
PROCsend_help(FNmessage_lookup("Hlp55"))
WHEN 10
PROCsend_help(FNmessage_lookup("Hlp56"))
WHEN 11
PROCsend_help(FNmessage_lookup("Hlp57"))
WHEN 12
PROCsend_help(FNmessage_lookup("Hlp58"))
WHEN 13
PROCsend_help(FNmessage_lookup("Hlp59"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp60"))
ENDCASE
WHEN csv%
CASE icon% OF
WHEN 0
PROCsend_help(FNmessage_lookup("Hlp61"))
WHEN 1
PROCsend_help(FNmessage_lookup("Hlp62"))
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp63"))
WHEN 3
PROCsend_help(FNmessage_lookup("Hlp64"))
WHEN 4
PROCsend_help(FNmessage_lookup("Hlp65"))
WHEN 5
PROCsend_help(FNmessage_lookup("Hlp66"))
WHEN 6
PROCsend_help(FNmessage_lookup("Hlp67"))
WHEN 7
PROCsend_help(FNmessage_lookup("Hlp68"))
WHEN 8
PROCsend_help(FNmessage_lookup("Hlp69"))
WHEN 9
PROCsend_help(FNmessage_lookup("Hlp70"))
WHEN 10
PROCsend_help(FNmessage_lookup("Hlp71"))
WHEN 11
PROCsend_help(FNmessage_lookup("Hlp72"))
WHEN 12
PROCsend_help(FNmessage_lookup("Hlp73"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp74"))
ENDCASE
WHEN print%
CASE icon% OF
WHEN 0
PROCsend_help(FNmessage_lookup("Hlp75"))
WHEN 1
PROCsend_help(FNmessage_lookup("Hlp76"))
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp77"))
WHEN 3
PROCsend_help(FNmessage_lookup("Hlp78"))
WHEN 4
PROCsend_help(FNmessage_lookup("Hlp79"))
WHEN 5
PROCsend_help(FNmessage_lookup("Hlp80"))
WHEN 6
PROCsend_help(FNmessage_lookup("Hlp81"))
WHEN 7
PROCsend_help(FNmessage_lookup("Hlp82"))
WHEN 8
PROCsend_help(FNmessage_lookup("Hlp83"))
WHEN 9
PROCsend_help(FNmessage_lookup("Hlp84"))
WHEN 10
PROCsend_help(FNmessage_lookup("Hlp85"))
WHEN 11
PROCsend_help(FNmessage_lookup("Hlp86"))
WHEN 12
PROCsend_help(FNmessage_lookup("Hlp87"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp88"))
ENDCASE
WHEN fileinf%
CASE icon% OF
WHEN 1,6
PROCsend_help(FNmessage_lookup("Hlp90"))
WHEN 2,7
PROCsend_help(FNmessage_lookup("Hlp91"))
WHEN 3,4,8
PROCsend_help(FNmessage_lookup("Hlp92"))
WHEN 5
PROCsend_help(FNmessage_lookup("Hlp93"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp94"))
ENDCASE
WHEN printset%
CASE icon% OF
WHEN 0
PROCsend_help(FNmessage_lookup("Hlp96"))
WHEN 1
PROCsend_help(FNmessage_lookup("Hlp97"))
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp98"))
WHEN 3
PROCsend_help(FNmessage_lookup("Hlp99"))
WHEN 4
PROCsend_help(FNmessage_lookup("Hlp100"))
WHEN 5
PROCsend_help(FNmessage_lookup("Hlp101"))
WHEN 6
PROCsend_help(FNmessage_lookup("Hlp102"))
WHEN 7
PROCsend_help(FNmessage_lookup("Hlp103"))
WHEN 8
PROCsend_help(FNmessage_lookup("Hlp104"))
WHEN 9
PROCsend_help(FNmessage_lookup("Hlp105"))
WHEN 10
PROCsend_help(FNmessage_lookup("Hlp106"))
WHEN 11
PROCsend_help(FNmessage_lookup("Hlp107"))
WHEN 12
PROCsend_help(FNmessage_lookup("Hlp108"))
WHEN 13
PROCsend_help(FNmessage_lookup("Hlp109"))
WHEN 14
PROCsend_help(FNmessage_lookup("Hlp110"))
WHEN 15
PROCsend_help(FNmessage_lookup("Hlp111"))
WHEN 16
PROCsend_help(FNmessage_lookup("Hlp112"))
WHEN 17
PROCsend_help(FNmessage_lookup("Hlp113"))
WHEN 18
PROCsend_help(FNmessage_lookup("Hlp114"))
WHEN 19
PROCsend_help(FNmessage_lookup("Hlp115"))
WHEN 20
PROCsend_help(FNmessage_lookup("Hlp116"))
WHEN 21
PROCsend_help(FNmessage_lookup("Hlp118"))
WHEN 22
PROCsend_help(FNmessage_lookup("Hlp119"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp117"))
ENDCASE
WHEN merge%
CASE icon% OF
WHEN 0
PROCsend_help(FNmessage_lookup("Hlp124"))
WHEN 1
PROCsend_help(FNmessage_lookup("Hlp125"))
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp126"))
WHEN 3
PROCsend_help(FNmessage_lookup("Hlp127"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp128"))
ENDCASE
WHEN loadcsv%
CASE icon% OF
WHEN 0
PROCsend_help(FNmessage_lookup("Hlp129"))
WHEN 1
PROCsend_help(FNmessage_lookup("Hlp130"))
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp131"))
WHEN 3
PROCsend_help(FNmessage_lookup("Hlp132"))
WHEN 4
PROCsend_help(FNmessage_lookup("Hlp133"))
WHEN 5
PROCsend_help(FNmessage_lookup("Hlp134"))
WHEN 6
PROCsend_help(FNmessage_lookup("Hlp135"))
WHEN 7
PROCsend_help(FNmessage_lookup("Hlp136"))
WHEN 8
PROCsend_help(FNmessage_lookup("Hlp137"))
WHEN 9
PROCsend_help(FNmessage_lookup("Hlp138"))
WHEN 10
PROCsend_help(FNmessage_lookup("Hlp139"))
WHEN 11
PROCsend_help(FNmessage_lookup("Hlp140"))
WHEN 12
PROCsend_help(FNmessage_lookup("Hlp141"))
WHEN 13
PROCsend_help(FNmessage_lookup("Hlp142"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp143"))
ENDCASE
WHEN prefs%
CASE icon% OF
WHEN 0
PROCsend_help(FNmessage_lookup("Hlp144"))
WHEN 1
PROCsend_help(FNmessage_lookup("Hlp145"))
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp146"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp147"))
ENDCASE
WHEN prefspane%
CASE icon% OF
WHEN 0,1
PROCsend_help(FNmessage_lookup("Hlp148"))
WHEN 2
PROCsend_help(FNmessage_lookup("Hlp149"))
WHEN 3
PROCsend_help(FNmessage_lookup("Hlp150"))
WHEN 4
PROCsend_help(FNmessage_lookup("Hlp151"))
WHEN 5,6
PROCsend_help(FNmessage_lookup("Hlp152"))
WHEN 7
PROCsend_help(FNmessage_lookup("Hlp153"))
WHEN 8
PROCsend_help(FNmessage_lookup("Hlp154"))
WHEN 9,10
PROCsend_help(FNmessage_lookup("Hlp155"))
WHEN 11
PROCsend_help(FNmessage_lookup("Hlp156"))
WHEN 12
PROCsend_help(FNmessage_lookup("Hlp157"))
WHEN 13
PROCsend_help(FNmessage_lookup("Hlp158"))
WHEN 14
PROCsend_help(FNmessage_lookup("Hlp159"))
WHEN 15
PROCsend_help(FNmessage_lookup("Hlp160"))
WHEN 16
PROCsend_help(FNmessage_lookup("Hlp161"))
WHEN 17
PROCsend_help(FNmessage_lookup("Hlp162"))
WHEN 18
PROCsend_help(FNmessage_lookup("Hlp163"))
WHEN 19
PROCsend_help(FNmessage_lookup("Hlp164"))
WHEN 20
PROCsend_help(FNmessage_lookup("Hlp165"))
WHEN 21,22
PROCsend_help(FNmessage_lookup("Hlp166"))
WHEN 23,24
PROCsend_help(FNmessage_lookup("Hlp167"))
WHEN 25,26
PROCsend_help(FNmessage_lookup("Hlp168"))
WHEN 27,28
PROCsend_help(FNmessage_lookup("Hlp169"))
WHEN 29,30
PROCsend_help(FNmessage_lookup("Hlp170"))
WHEN 31,32
PROCsend_help(FNmessage_lookup("Hlp171"))
WHEN 33
PROCsend_help(FNmessage_lookup("Hlp172"))
WHEN 34
PROCsend_help(FNmessage_lookup("Hlp184"))
WHEN 35
PROCsend_help(FNmessage_lookup("Hlp185"))
WHEN 36
PROCsend_help(FNmessage_lookup("Hlp186"))
WHEN 37
PROCsend_help(FNmessage_lookup("Hlp187"))
WHEN 43
PROCsend_help(FNmessage_lookup("Hlp189"))
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp173"))
ENDCASE
OTHERWISE
PROCsend_help(FNmessage_lookup("Hlp188"))
ENDCASE
ENDPROC
:
DEF PROCsend_help(help$)
b%!12=b%!8
b%!16=&503
$(b%+20)=help$+STRING$(4,CHR$(0))
!b%=24+(LEN(help$) AND &FFFFFC)
SYS "Wimp_SendMessage",17,b%,b%!4
ENDPROC
:
DEF FNmenu_state(handle%,item%)
LOCAL a%
a%=handle%+28+(item%*24)
=(?a% AND 1)
:
DEF PROCshade_button(whand%,ihand%)
LOCAL eor%,clear%
eor%=0
clear%=(255<<24)+(15<<12)
!q%=whand%
q%!4=ihand%
q%!8=eor%
q%!12=clear%
SYS "Wimp_SetIconState",,q%
eor%=0
clear%=0
eor%+=(0<<12)
eor%+=(3<<24)+(1<<28)
clear%=eor%
q%!8=eor%
q%!12=clear%
SYS "Wimp_SetIconState",,q%
ENDPROC
:
DEF PROCclear_button(whand%,ihand%)
LOCAL eor%,clear%
eor%=0
clear%=(255<<24)+(15<<12)
!q%=whand%
q%!4=ihand%
q%!8=eor%
q%!12=clear%
SYS "Wimp_SetIconState",,q%
eor%=0
clear%=0
eor%+=(3<<12)
eor%+=(7<<24)+(1<<28)
clear%=eor%
q%!8=eor%
q%!12=clear%
SYS "Wimp_SetIconState",,q%
ENDPROC
:
DEF PROCredraw_window(whand%)
!q%=whand%
SYS "Wimp_GetWindowState",,q%
SYS "Wimp_ForceRedraw",whand%,q%!20,(q%!24)-(q%!16-q%!8),(q%!20)+(q%!12-q%!4),q%!24
SYS "Wimp_ForceRedraw",-1,q%!4,q%!16,q%!12+40,q%!16+40
ENDPROC
:
DEF PROCredraw_part(h%,x%,y%,a%,b%)
SYS "Wimp_ForceRedraw",h%,x%,y%,x%+a%,y%+b%
ENDPROC
:
DEF PROCcheck_select(w%,i%)
IF NOT (FNicon_selected(w%,i%)<>0) THENPROCicon_set_state(w%,i%,1,0,0)
ENDPROC
:
DEF PROCopen_window(whan%)
!b%=whan%
SYS "Wimp_GetWindowState",,b%
IF !b%=MainWindow% OR !b%=prefs% THEN
IF !b%=MainWindow% THEN
!q%=TBarWindow%
SYS "Wimp_GetWindowState",,q%
q%!4=b%!4
q%!16=b%!16
IF FNmenu_state(MainMenu%,8) THENq%!8=b%!16-136 ELSEq%!8=b%!16-36
q%!12=b%!12
q%!20=b%!20
IF FNmenu_state(MainMenu%,8) THENq%!24=0 ELSEq%!24=-96
q%!28=b%!28
SYS "Wimp_OpenWindow",,q%
IF FNmenu_state(MainMenu%,7) AND join_main% THEN
!q%=enter%
SYS "Wimp_GetWindowState",,q%
q%!4=b%!4+40
IF b%!8>236 THENover%=0
IF b%!8<=236 AND b%!8>0 THENover%=236-b%!8
IF b%!8<=0 THENover%=236
q%!16=b%!8-44+over%
q%!8=b%!8-236+over%
q%!12=b%!4+1236
q%!20=0
q%!24=0
q%!28=TBarWindow%
SYS "Wimp_OpenWindow",,q%
b%!28=enter%
ELSE
b%!28=TBarWindow%
ENDIF
SYS "Wimp_OpenWindow",,b%
ELSE
!q%=prefspane%
SYS "Wimp_GetWindowState",,q%
q%!4=b%!4+22
q%!16=b%!8+402
q%!8=b%!8+24
q%!12=b%!4+502
q%!20=0
q%!24=0
q%!28=b%!28
SYS "Wimp_OpenWindow",,q%
b%!28=prefspane%
SYS "Wimp_OpenWindow",,b%
ENDIF
ELSE
SYS "Wimp_OpenWindow",,b%
ENDIF
ENDPROC


REM Test an error to see if it is one that we recognise and can hande.
REM
REM \param err%			The error code to be tested.
REM \return			TRUE if the error was recogniosed; FALSE if not.
:
DEF FNcheck_error(err%)
LOCAL token$, save_error%

save_error% = FALSE
token$ = ""

CASE err% OF
WHEN 20
	token$ = "Err1"
WHEN 204
	token$ = "Err2"
	save_error% = TRUE
WHEN 214
	token$ = "Err11"
	save_error% = TRUE
WHEN 223
	token$ = "Err3" : CLOSE#i%
WHEN 275
	token$ = "Err10"
	save_error% = TRUE
WHEN 1491
	token$ = "Err4"
WHEN 67796
	token$ = "Err13"
	save_error% = TRUE
WHEN 67797
	token$ = "Err5"
	save_error% = TRUE
WHEN 71603
	token$ = "Err12"
	save_error% = TRUE
WHEN 71613
	token$ = "Err6"
WHEN 71618
	token$ = "Err7"
	save_error% = TRUE
WHEN 71622
	token$ = "Err8"
	save_error% = TRUE
WHEN 71636
	token$ = "Err9"
	save_error% = TRUE
ENDCASE

REM We don't recognise the error, so exit without claiming it.

IF token$ == "" THEN =FALSE

REM It's an error code we recognise, so report it and exit showing that
REM we've claimed the error.

PROCwimperror_report(FNmessage_lookup(token$), 0, 1, "")
IF save_error% THEN PROCwimperror_report(FNmessage_lookup("Fil3"), 0, 2, "")

=TRUE

